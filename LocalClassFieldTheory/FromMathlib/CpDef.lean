/-
Copyright (c) 2023 MarÃ­a InÃ©s de Frutos-FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: MarÃ­a InÃ©s de Frutos-FernÃ¡ndez
-/
import Mathlib.NumberTheory.Padics.PadicNumbers
import Mathlib.RingTheory.Valuation.Integers
import Mathlib.RingTheory.Valuation.RankOne
import Mathlib.Topology.MetricSpace.CauSeqFilter
import Mathlib.Topology.Algebra.Valued.NormedValued
import Mathlib.Topology.Algebra.Valued.ValuedField
import LocalClassFieldTheory.FromMathlib.SpectralNormUnique

#align_import from_mathlib.Cp_def

/-!
# The `p`-adic complex numbers.

In this file we define the field `â„‚_[p]` of `p`-adic complex numbers and we give it both a normed
field and a valued field structure, induced by the unique extension of the `p`-adic norm to `â„‚_[p]`.

## Main Definitions

* `padic_complex` : the type of `p`-adic complex numbers.
* `padic_complex_integers` : the ring of integers of `â„‚_[p]`.


## Main Results

* `padic_complex.norm_extends` : the norm on `â„‚_[p]` extends the norm on `Q_p_alg p`, and hence
  the norm on `â„š_[p]`.
* `padic_complex.is_nonarchimedean` : The norm on `â„‚_[p]` is nonarchimedean.


## Notation

We introduce the notation `â„‚_[p]` for the `p`-adic complex numbers, and `ğ“_â„‚_[p]` for its ring of
integers.

## Tags

p-adic, p adic, padic, norm, valuation, cauchy, completion, p-adic completion
-/


noncomputable section

open Valued Valuation

open scoped NNReal

variable (p : â„•) [hp: Fact (Nat.Prime p)]

/-- `Q_p_alg p` is the algebraic closure of `â„š_[p]`. -/
@[reducible]
def QPAlg : Type _ :=
  AlgebraicClosure â„š_[p]

/-- `Q_p_alg p` is an algebraic extension of `â„š_[p]`. -/
theorem QPAlg.isAlgebraic : Algebra.IsAlgebraic â„š_[p] (QPAlg p) :=
  AlgebraicClosure.isAlgebraic _

instance : Coe â„š_[p] (QPAlg p) := âŸ¨algebraMap â„š_[p] (QPAlg p)âŸ©

theorem coe_eql : (Coe.coe : â„š_[p] â†’ QPAlg p) = algebraMap â„š_[p] (QPAlg p) := by
  rfl


namespace QPAlg

/-- `Q_p_alg p` is a normed field, where the norm is the `p`-adic norm, that is, the spectral norm
induced by the `p`-adic norm on `â„š_[p]`. -/
instance normedField : NormedField (QPAlg p) :=
  spectralNormToNormedField (K := â„š_[p]) (L := QPAlg p) padicNormE.nonarchimedean

/-- The norm on `Q_p_alg p` is nonarchimedean. -/
theorem isNonarchimedean : IsNonarchimedean (norm : QPAlg p â†’ â„) :=
  spectralNorm_isNonarchimedean (K := â„š_[p]) (L := QPAlg p) padicNormE.nonarchimedean

/-- The norm on `Q_p_alg p` is the spectral norm induced by the `p`-adic norm on `â„š_[p]`. -/
theorem norm_def (x : QPAlg p) : â€–xâ€– = spectralNorm â„š_[p] (QPAlg p) x :=
  rfl

/-- The norm on `Q_p_alg p` extends the `p`-adic norm on `â„š_[p]`. -/
theorem QP.norm_extends (x : â„š_[p]) : â€–(x : QPAlg p)â€– = â€–xâ€– :=
  spectralAlgNorm_extends (K := â„š_[p]) (L := QPAlg p) _ padicNormE.nonarchimedean

/-- `Q_p_alg p` is a valued field, with the valuation corresponding to the `p`-adic norm. -/
instance valuedField : Valued (QPAlg p) â„â‰¥0 :=
  NormedField.toValued (QPAlg.isNonarchimedean p)

/-- The valuation of `x : Q_p_alg p` agrees with its `â„â‰¥0`-valued norm. -/
theorem v_def (x : QPAlg p) : Valued.v x = â€–xâ€–â‚Š :=
  rfl

/-- The coercion of the valuation of `x : Q_p_alg p` to `â„` agrees with its norm. -/
theorem v_def_coe (x : QPAlg p) : ((Valued.v x : â„â‰¥0) : â„) = spectralNorm â„š_[p] (QPAlg p) x :=
  rfl

/-- The valuation of `p : Q_p_alg p` is `1/p`. -/
theorem valuation_p (p : â„•) [Fact p.Prime] : Valued.v (p : QPAlg p) = 1 / (p : â„â‰¥0) :=
  by
  rw [â† map_natCast (algebraMap â„š_[p] (QPAlg p))]
  ext
  rw [v_def_coe,  spectralNorm_extends, padicNormE.norm_p, one_div, NNReal.coe_inv,
    NNReal.coe_natCast]

end QPAlg

/-- `â„‚_[p]` is the field of `p`-adic complex numbers, that is, the completion of `Q_p_alg p` with
respect to the `p`-adic norm. -/
def PadicComplex :=
  UniformSpace.Completion (QPAlg p)

notation "â„‚_[" p "]" => PadicComplex p

namespace PadicComplex

/-- The `p`-adic complex numbers have a field structure. -/
instance : Field â„‚_[p] :=
  UniformSpace.Completion.instField

/-- The `p`-adic complex numbers are inhabited. -/
instance : Inhabited â„‚_[p] :=
  âŸ¨0âŸ©

/-- `â„‚_[p]` is a valued field, where the valuation is the one extending that on `Q_p_alg p`. -/
instance valuedField : Valued â„‚_[p] â„â‰¥0 :=
  Valued.valuedCompletion

/-- `â„‚_[p]` is a complete space. -/
instance completeSpace : CompleteSpace â„‚_[p] :=
  UniformSpace.Completion.completeSpace _

instance : Coe (QPAlg p) â„‚_[p] := âŸ¨UniformSpace.Completion.coe' _âŸ©

/-- The valuation on `â„‚_[p]` extends the valuation on `Q_p_alg p`. -/
theorem valuation_extends (x : QPAlg p) : Valued.v (x : â„‚_[p]) = Valued.v x :=
  Valued.extension_extends _

/-- `â„‚_[p]` is an algebra over `Q_p_alg p`. -/
instance : Algebra (QPAlg p) â„‚_[p] :=
  UniformSpace.Completion.algebra' _

theorem coe_eq : (Coe.coe : QPAlg p â†’ â„‚_[p]) = algebraMap (QPAlg p) â„‚_[p] :=
  rfl

theorem coe_zero : ((0 : QPAlg p) : â„‚_[p]) = 0 :=
  rfl

/-- The valuation of `p : â„‚_[p]` is `1/p`. -/
theorem valuation_p (p : â„•) [Fact p.Prime] : Valued.v (p : â„‚_[p]) = 1 / (p : â„â‰¥0) := by
  erw [â† map_natCast (algebraMap (QPAlg p) â„‚_[p]), â† coe_eq, valuation_extends, QPAlg.valuation_p]

/-- The valuation on `â„‚_[p]` has rank one. -/
instance : RankOne (PadicComplex.valuedField p).v where
  hom         := MonoidWithZeroHom.id â„â‰¥0
  strictMono' := strictMono_id
  nontrivial' := by
    use p
    haveI hp : Nat.Prime p := hp.1
    simp only [valuation_p, one_div, ne_eq, inv_eq_zero, Nat.cast_eq_zero, inv_eq_one,
      Nat.cast_eq_one]
    exact âŸ¨hp.ne_zero, hp.ne_oneâŸ©

/-- `â„‚_[p]` is a normed field, where the norm corresponds to the extension of the `p`-adic
  valuation.-/
instance C_p.NormedField : NormedField â„‚_[p] :=
  Valued.toNormedField _ _

instance : NormedField (UniformSpace.Completion (QPAlg p)) := C_p.NormedField p

/-- The norm on `â„‚_[p]` agrees with the valuation. -/
theorem normDef : (Norm.norm : â„‚_[p] â†’ â„) = Valued.norm := rfl

/-- The norm on `â„‚_[p]` extends the norm on `Q_p_alg p`. -/
theorem norm_extends (x : QPAlg p) : â€–(x : â„‚_[p])â€– = â€–xâ€– := by
  by_cases hx : x = 0
  Â· rw [hx, coe_zero, norm_zero, norm_zero]
  Â· erw [normDef, Valued.norm, valuation_extends, MonoidWithZeroHom.coe_mk]
    rfl

/-- The `â„â‰¥0`-valued norm on `â„‚_[p]` extends that on `Q_p_alg p`. -/
theorem nnnorm_extends (x : QPAlg p) : â€–(x : â„‚_[p])â€–â‚Š = â€–xâ€–â‚Š := by ext; exact norm_extends p x

/-- The norm on `â„‚_[p]` is nonarchimedean. -/
theorem isNonarchimedean : IsNonarchimedean (Norm.norm : â„‚_[p] â†’ â„) := by
  intro x y
  refine' UniformSpace.Completion.induction_onâ‚‚ x y _ _
  Â· exact
      isClosed_le (Continuous.comp continuous_norm continuous_add)
        (Continuous.max (Continuous.comp (@continuous_norm â„‚_[p] _) (Continuous.fst continuous_id))
          (Continuous.comp (@continuous_norm â„‚_[p] _) (Continuous.snd continuous_id)))
  Â· intro a b
    rw [â† UniformSpace.Completion.coe_add, norm_extends, norm_extends, norm_extends]
    exact QPAlg.isNonarchimedean p a b

  /- apply UniformSpace.Completion.induction_onâ‚‚ x y
  Â·
    exact
      isClosed_le (Continuous.comp continuous_norm continuous_add)
        (Continuous.max (Continuous.comp (@continuous_norm â„‚_[p] _) (Continuous.fst continuous_id))
          (Continuous.comp (@continuous_norm â„‚_[p] _) (Continuous.snd continuous_id)))
  Â· intro a b
    simp only [â† UniformSpace.Completion.coe_add, norm_extends]
    exact QPAlg.isNonarchimedean p a b -/

end PadicComplex

/-- We define `ğ“_â„‚_[p]` as the subring elements of `â„‚_[p]` with valuation `â‰¤ 1`. -/
def padicComplexIntegers : Subring â„‚_[p] :=
  (PadicComplex.valuedField p).v.integer

notation "ğ“_â„‚_[" p "]" => padicComplexIntegers p

/-- `ğ“_â„‚_[p]` is the ring of integers of `â„‚_[p]`. -/
theorem PadicComplex.integers : Valuation.Integers (PadicComplex.valuedField p).v ğ“_â„‚_[p] :=
  Valuation.integer.integers _
