/-
Copyright (c) 2023 Mar√≠a In√©s de Frutos-Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mar√≠a In√©s de Frutos-Fern√°ndez
-/
import Mathlib.Analysis.NormedSpace.BoundedLinearMaps
import Mathlib.LinearAlgebra.FiniteDimensional
import LocalClassFieldTheory.FromMathlib.SeminormFromBounded
import LocalClassFieldTheory.FromMathlib.SmoothingSeminorm

import Mathlib.LinearAlgebra.FiniteDimensional

#align_import from_mathlib.normed_space

/-!
# Basis.norm

In this file, we prove [BGR, Lemma 3.2.1./3]: if `K` is a normed field with a nonarchimedean
power-multiplicative norm and `L/K` is a finite extension, then there exists at least one
power-multiplicative `K`-algebra norm on `L` extending the norm on `K`.

## Main Definitions
* `basis.norm` : the function sending an element `x : L` to the maximum of the norms of its
  coefficients with respect to the `K`-basis `B` of `L`.

## Main Results
* `finite_extension_pow_mul_seminorm` : the proof of [BGR, Lemma 3.2.1./3].

## References
* [S. Bosch, U. G√ºntzer, R. Remmert, *Non-Archimedean Analysis*][bosch-guntzer-remmert]

## Tags

basis.norm, nonarchimedean
-/

set_option autoImplicit false


noncomputable section

open Finset

open scoped BigOperators

section Continuous

/-- A continuous linear map between normed spaces. -/
structure IsContinuousLinearMap (ùïú : Type _) [NormedField ùïú] {E : Type _} [NormedAddCommGroup E]
    [NormedSpace ùïú E] {F : Type _} [NormedAddCommGroup F] [NormedSpace ùïú F] (f : E ‚Üí F) extends
    IsLinearMap ùïú f : Prop where
  cont : Continuous f := by continuity

/-- A linear map between normed spaces is continuous if and only if it is bounded,-/
theorem isContinuousLinearMap_iff_isBoundedLinearMap {K : Type _} [NontriviallyNormedField K]
    {M : Type _} [NormedAddCommGroup M] [NormedSpace K M] {N : Type _} [NormedAddCommGroup N]
    [NormedSpace K N] (f : M ‚Üí N) : IsContinuousLinearMap K f ‚Üî IsBoundedLinearMap K f := by
  refine' ‚ü®fun h_cont => _, fun h_bdd => ‚ü®h_bdd.toIsLinearMap, h_bdd.continuous‚ü©‚ü©
  ¬∑ set F : M ‚ÜíL[K] N := by
      use ‚ü®‚ü®f, IsLinearMap.map_add h_cont.1‚ü©, IsLinearMap.map_smul h_cont.1‚ü©
      exact h_cont.2
    exact ContinuousLinearMap.isBoundedLinearMap F

end Continuous

section finsum

/-- Given a function `f : Œ± ‚Üí M` and a linear equivalence `g : M ‚âÉ‚Çõ‚Çó[œÉ] N`, we have
  `g ‚àë·∂† f i = ‚àë·∂† g(f i)`.  -/
theorem LinearEquiv.map_finsum {R S : Type _} {Œ± : Sort _} [Semiring R] [Semiring S] (œÉ : R ‚Üí+* S)
    {œÉ' : S ‚Üí+* R} [RingHomInvPair œÉ œÉ'] [RingHomInvPair œÉ' œÉ] (M N : Type _) [AddCommMonoid M]
    [AddCommMonoid N] [Module R M] [Module S N] (g : M ‚âÉ‚Çõ‚Çó[œÉ] N) (f : Œ± ‚Üí M) :
    g (finsum fun i : Œ± => f i) = finsum fun i : Œ± => g (f i) :=
  AddEquiv.map_finsum g.toAddEquiv f

/-- Given a fintype `Œ±`, a function `f : Œ± ‚Üí M` and a linear equivalence `g : M ‚âÉ‚Çõ‚Çó[œÉ] N`, we have
  `g (‚àë (i : Œ±), f i) = ‚àë (i : Œ±), g (f i)`.  -/
theorem LinearEquiv.map_finset_sum {R S : Type _} {Œ± : Sort _} [Fintype Œ±] [Semiring R] [Semiring S]
    (œÉ : R ‚Üí+* S) {œÉ' : S ‚Üí+* R} [RingHomInvPair œÉ œÉ'] [RingHomInvPair œÉ' œÉ] (M N : Type _)
    [AddCommMonoid M] [AddCommMonoid N] [Module R M] [Module S N] (g : M ‚âÉ‚Çõ‚Çó[œÉ] N) (f : Œ± ‚Üí M) :
    g (‚àë i : Œ±, f i) = ‚àë i : Œ±, g (f i) := by
  simp only [‚Üê finsum_eq_sum_of_fintype, LinearEquiv.map_finsum]

theorem finsum_apply {Œ± : Type _} {Œ≤ : Œ± ‚Üí Type _} {Œ≥ : Type _} [Finite Œ≥]
    [‚àÄ a : Œ±, AddCommMonoid (Œ≤ a)] (a : Œ±) (g : Œ≥ ‚Üí ‚àÄ a : Œ±, Œ≤ a) :
    finsum (fun c : Œ≥ => g c) a = finsum fun c : Œ≥ => g c a :=
  by
  cases nonempty_fintype Œ≥
  simp only [finsum_eq_sum_of_fintype, sum_apply]

/-- If `‚àë i, f i ‚Ä¢ v i = ‚àë i, g i ‚Ä¢ v i`, then for all `i`, `f i = g i`. -/
theorem LinearIndependent.eq_coords_of_eq {R : Type _} [Ring R] {M : Type _} [AddCommGroup M]
    [Module R M] {Œπ : Type _} [Fintype Œπ] {v : Œπ ‚Üí M} (hv : LinearIndependent R v) {f : Œπ ‚Üí R}
    {g : Œπ ‚Üí R} (heq : ‚àë i, f i ‚Ä¢ v i = ‚àë i, g i ‚Ä¢ v i) (i : Œπ) : f i = g i :=
  by
  rw [‚Üê sub_eq_zero, ‚Üê sum_sub_distrib] at heq
  simp_rw [‚Üê sub_smul] at heq
  rw [linearIndependent_iff'] at hv
  exact sub_eq_zero.mp (hv univ (fun i => f i - g i) heq i (mem_univ i))

end finsum

variable {K : Type _} [NormedField K] {L : Type _} [Field L] [Algebra K L]

/-- If `B` is a basis of the `K`-vector space `L` such that `B i = 1` for some index `i`, then
each `k:K` gets represented as `k ‚Ä¢ B i` as an element of `L`. -/
theorem basis_one {Œπ : Type _} [Fintype Œπ] [DecidableEq Œπ] {B : Basis Œπ K L} {i : Œπ}
    (hBi : B i = (1 : L)) (k : K) :
    B.equivFun ((algebraMap K L) k) = fun j : Œπ => if j = i then k else 0 := by
  ext j
  apply LinearIndependent.eq_coords_of_eq B.linearIndependent
  rw [Basis.sum_equivFun B (algebraMap K L k)]
  have h_sum : ‚àë j : Œπ, ite (j = i) k 0 ‚Ä¢ B j = ‚àë j : Œπ, ite (j = i) (k ‚Ä¢ B j) 0 := by
    apply sum_congr (Eq.refl _)
    ¬∑ rintro h -
      split_ifs
      exacts [rfl, zero_smul _ _]
  rw [h_sum, Algebra.algebraMap_eq_smul_one, sum_ite_eq' univ (i : Œπ) fun j : Œπ => k ‚Ä¢ B j]
  simp only [mem_univ, if_true, hBi]

namespace Basis

/-- The function sending an element `x : L` to the maximum of the norms of its coefficients
  with respect to the `K`-basis `B` of `L`.-/
def norm {Œπ : Type _} [Fintype Œπ] [Nonempty Œπ] (B : Basis Œπ K L) : L ‚Üí ‚Ñù := fun x =>
  ‚ÄñB.equivFun x (Classical.choose (Finite.exists_max fun i : Œπ => ‚ÄñB.equivFun x i‚Äñ))‚Äñ

/-- The norm of a coefficient `x_i` is less than or equal to the norm of `x`. -/
theorem le_norm {Œπ : Type _} [Fintype Œπ] [Nonempty Œπ] (B : Basis Œπ K L) (x : L) (i : Œπ) :
    ‚ÄñB.equivFun x i‚Äñ ‚â§ B.norm x :=
  Classical.choose_spec (Finite.exists_max fun i : Œπ => ‚ÄñB.equivFun x i‚Äñ) i

/-- For any `K`-basis of `L`, we have `B.norm 0 = 0`. -/
theorem norm_zero' {Œπ : Type _} [Fintype Œπ] [Nonempty Œπ] (B : Basis Œπ K L) : B.norm 0 = 0 := by
  simp only [norm, map_zero, Pi.zero_apply, norm_zero]

/-- For any `K`-basis of `L`, and any `x : L`, we have `B.norm (-x) = B.norm x`. -/
theorem norm_neg {Œπ : Type _} [Fintype Œπ] [Nonempty Œπ] (B : Basis Œπ K L) (x : L) :
    B.norm (-x) = B.norm x := by
  simp only [norm, map_neg, equivFun_apply,  Pi.neg_apply, _root_.norm_neg]

/-- For any `K`-basis of `L`, `B.norm` extends the norm on `K`. -/
theorem norm_extends {Œπ : Type _} [Fintype Œπ] [Nonempty Œπ] {B : Basis Œπ K L} {i : Œπ}
    (hBi : B i = (1 : L)) : FunctionExtends (fun x : K => ‚Äñx‚Äñ) B.norm := by
  classical
  intro k
  ¬∑ by_cases hk : k = 0
    ¬∑ simp only [hk, map_zero, norm_zero, norm_zero']
    ¬∑ simp only [norm, basis_one hBi]
      have h_max : Classical.choose
            (Finite.exists_max fun j : Œπ => ‚Äñ(fun n : Œπ => if n = i then k else 0) j‚Äñ) = i := by
        by_contra h
        have h_max :=
          Classical.choose_spec
            (Finite.exists_max fun j : Œπ => ‚Äñ(fun n : Œπ => if n = i then k else 0) j‚Äñ)
        simp only [if_neg h] at h_max
        specialize h_max i
        rw [if_pos rfl, norm_zero, norm_le_zero_iff] at h_max
        exact hk h_max
      rw [if_pos h_max]

/-- For any `K`-basis of `L`, if the norm on `K` is nonarchimedean, then so is `B.norm`. -/
theorem norm_isNonarchimedean {Œπ : Type _} [Fintype Œπ] [Nonempty Œπ] {B : Basis Œπ K L}
    (hna : IsNonarchimedean (Norm.norm : K ‚Üí ‚Ñù)) : IsNonarchimedean B.norm := by
  classical
  intro x y
  simp only [Basis.norm]
  set ixy := Classical.choose (Finite.exists_max fun i : Œπ => ‚ÄñB.equivFun (x + y) i‚Äñ)
  have hxy : ‚ÄñB.equivFun (x + y) ixy‚Äñ ‚â§ max ‚ÄñB.equivFun x ixy‚Äñ ‚ÄñB.equivFun y ixy‚Äñ := by
    rw [LinearEquiv.map_add, Pi.add_apply]; exact hna _ _
  have hix := Classical.choose_spec (Finite.exists_max fun i : Œπ => ‚ÄñB.equivFun x i‚Äñ)
  have hiy := Classical.choose_spec (Finite.exists_max fun i : Œπ => ‚ÄñB.equivFun y i‚Äñ)
  cases' le_max_iff.mp hxy with hx hy
  ¬∑ apply le_max_of_le_left (le_trans hx (hix ixy))
  ¬∑ apply le_max_of_le_right (le_trans hy (hiy ixy))

/-- For any `K`-basis of `L`, `B.norm` is bounded with respect to multiplication. That is,
  `‚àÉ (c : ‚Ñù), c > 0` such that ` ‚àÄ (x y : L), B.norm (x * y) ‚â§ c * B.norm x * B.norm y`. -/
theorem norm_is_bdd {Œπ : Type _} [Fintype Œπ] [Nonempty Œπ] {B : Basis Œπ K L} {i : Œπ}
    (hBi : B i = (1 : L)) (hna : IsNonarchimedean (Norm.norm : K ‚Üí ‚Ñù)) :
    ‚àÉ (c : ‚Ñù) (_ : 0 < c), ‚àÄ x y : L, B.norm (x * y) ‚â§ c * B.norm x * B.norm y := by
  classical
  -- The bounding constant `c` will be the maximum of the products `B.norm (B i * B j)`.
  set c := Classical.choose (Finite.exists_max fun i : Œπ √ó Œπ => B.norm (B i.1 * B i.2))
  have hc := Classical.choose_spec (Finite.exists_max fun i : Œπ √ó Œπ => B.norm (B i.1 * B i.2))
  use B.norm (B c.1 * B c.2)
  constructor
  -- ‚àÄ (x y : L), B.norm (x * y) ‚â§ B.norm (‚áëB c.fst * ‚áëB c.snd) * B.norm x * B.norm y
  ¬∑ intro x y
    -- `ixy` is an index for which `‚ÄñB.equivFun (x*y) i‚Äñ` is maximum.
    set ixy := Classical.choose (Finite.exists_max fun i : Œπ => ‚ÄñB.equivFun (x * y) i‚Äñ) with
      hixy_def
    -- We rewrite the LHS using `ixy`.
    conv_lhs =>
      simp only [Basis.norm]
      rw [‚Üê hixy_def, ‚Üê Basis.sum_equivFun B x, ‚Üê Basis.sum_equivFun B y]
    rw [sum_mul, LinearEquiv.map_finset_sum, sum_apply]
    simp_rw [smul_mul_assoc, LinearEquiv.map_smul, mul_sum, LinearEquiv.map_finset_sum,
      mul_smul_comm, LinearEquiv.map_smul]
    have hna' : IsNonarchimedean (NormedField.toMulRingNorm K) := hna
    /- Since the norm is nonarchimidean, the norm of a finite sum is bounded by the maximum of the
          norms of the summands. -/
    have hk : ‚àÉ (k : Œπ) (_ : (univ : Finset Œπ).Nonempty ‚Üí k ‚àà univ ),
        ‚Äñ‚àë i : Œπ, (B.equivFun x i ‚Ä¢ ‚àë i_1 : Œπ, B.equivFun y i_1 ‚Ä¢ B.equivFun (B i * B i_1)) ixy‚Äñ ‚â§
          ‚Äñ(B.equivFun x k ‚Ä¢ ‚àë j : Œπ, B.equivFun y j ‚Ä¢ B.equivFun (B k * B j)) ixy‚Äñ :=
      isNonarchimedean_finset_image_add hna'
        (fun i => (B.equivFun x i ‚Ä¢ ‚àë i_1 : Œπ, B.equivFun y i_1 ‚Ä¢ B.equivFun (B i * B i_1)) ixy)
        (univ : Finset Œπ)
    obtain ‚ü®k, -, hk‚ü© := hk
    apply le_trans hk
    -- We use the above property again.
    have hk' : ‚àÉ (k' : Œπ) (_ : (univ : Finset Œπ).Nonempty ‚Üí k' ‚àà univ),
        ‚Äñ‚àë j : Œπ, B.equivFun y j ‚Ä¢ B.equivFun (B k * B j) ixy‚Äñ ‚â§
          ‚ÄñB.equivFun y k' ‚Ä¢ B.equivFun (B k * B k') ixy‚Äñ :=
      isNonarchimedean_finset_image_add hna'
        (fun i => B.equivFun y i ‚Ä¢ B.equivFun (B k * B i) ixy) (univ : Finset Œπ)
    obtain ‚ü®k', -, hk'‚ü© := hk'
    rw [Pi.smul_apply, norm_smul, sum_apply]
    apply le_trans (mul_le_mul_of_nonneg_left hk' (norm_nonneg _))
    -- Now an easy computation leads to the desired conclusion.
    rw [norm_smul, mul_assoc, mul_comm (B.norm (B c.fst * B c.snd)), ‚Üê mul_assoc]
    exact
      mul_le_mul (mul_le_mul (B.le_norm _ _) (B.le_norm _ _) (norm_nonneg _) (norm_nonneg _))
        (le_trans (B.le_norm _ _) (hc (k, k'))) (norm_nonneg _)
        (mul_nonneg (norm_nonneg _) (norm_nonneg _))
    -- `B c.1 * B c.2` is positive
  ¬∑ have h_pos : (0 : ‚Ñù) < B.norm (B i * B i) :=
      by
      have h1 : (1 : L) = (algebraMap K L) 1 := by rw [map_one]
      rw [hBi, mul_one, h1, Basis.norm_extends hBi]
      simp only [norm_one, zero_lt_one]
    exact lt_of_lt_of_le h_pos (hc (i, i))

/-- For any `k : K`, `y : L`, we have
  `B.equivFun ((algebra_map K L k) * y) i = k * (B.equivFun y i) `. -/
theorem repr_smul {Œπ : Type _} [Fintype Œπ] (B : Basis Œπ K L) (i : Œπ) (k : K) (y : L) :
    B.equivFun (algebraMap K L k * y) i = k * B.equivFun y i := by
  rw [‚Üê smul_eq_mul, algebraMap_smul, LinearEquiv.map_smul]; rfl

/-- For any `k : K`, `y : L`, we have
  `B.norm ((algebra_map K L) k * y) = B.norm ((algebra_map K L) k) * B.norm y`. -/
theorem norm_smul {Œπ : Type _} [Fintype Œπ] [Nonempty Œπ] {B : Basis Œπ K L} {i : Œπ}
    (hBi : B i = (1 : L)) (k : K) (y : L) :
    B.norm ((algebraMap K L) k * y) = B.norm ((algebraMap K L) k) * B.norm y := by
  classical
  by_cases hk : k = 0
  ¬∑ rw [hk, map_zero, MulZeroClass.zero_mul, B.norm_zero', MulZeroClass.zero_mul]
  ¬∑ rw [norm_extends hBi]
    simp only [norm]
    set i := Classical.choose (Finite.exists_max fun i : Œπ => ‚ÄñB.equivFun y i‚Äñ)
    have hi := Classical.choose_spec (Finite.exists_max fun i : Œπ => ‚ÄñB.equivFun y i‚Äñ)
    set j :=
      Classical.choose
        (Finite.exists_max fun i : Œπ => ‚ÄñB.equivFun ((algebraMap K L) k * y) i‚Äñ) with
      hj_def
    have hj :=
      Classical.choose_spec
        (Finite.exists_max fun i : Œπ => ‚ÄñB.equivFun ((algebraMap K L) k * y) i‚Äñ)
    have hij : ‚ÄñB.equivFun y i‚Äñ = ‚ÄñB.equivFun y j‚Äñ := by
      refine' le_antisymm _ (hi j)
      ¬∑ specialize hj i
        rw [‚Üê hj_def, repr_smul, norm_mul, repr_smul, norm_mul] at hj
        exact (mul_le_mul_left (lt_of_le_of_ne (norm_nonneg _)
          (Ne.symm (norm_ne_zero_iff.mpr hk)))).mp hj
    rw [repr_smul, norm_mul, hij]

end Basis

/-- If `K` is a nonarchimedean normed field `L/K` is a finite extension, then there exists a
power-multiplicative nonarchimedean `K`-algebra norm on `L` extending the norm on `K`. -/
theorem finite_extension_pow_mul_seminorm (hfd : FiniteDimensional K L)
    (hna : IsNonarchimedean (norm : K ‚Üí ‚Ñù)) :
    ‚àÉ f : AlgebraNorm K L, IsPowMul f ‚àß FunctionExtends (norm : K ‚Üí ‚Ñù) f ‚àß IsNonarchimedean f := by
  classical
  -- Choose a basis B = {1, e2,..., en} of the K-vector space L
  set h1 : LinearIndependent K fun x : ({1} : Set L) => (x : L) :=
    linearIndependent_singleton one_ne_zero
  set Œπ := { x // x ‚àà h1.extend (Set.subset_univ ({1} : Set L)) }
  set B : Basis Œπ K L := Basis.extend h1
  letI hfin : Fintype Œπ := FiniteDimensional.fintypeBasisIndex B
  haveI hem : Nonempty Œπ := B.index_nonempty
  have h1L : (1 : L) ‚àà h1.extend _ := Basis.subset_extend _ (Set.mem_singleton (1 : L))
  have hB1 : B ‚ü®1, h1L‚ü© = (1 : L) := by rw [Basis.coe_extend, Subtype.coe_mk]
  -- For every k ‚àà K, k = k ‚Ä¢ 1 + 0 ‚Ä¢ e2 + ... + 0 ‚Ä¢ en
 /-  have h_k :
    ‚àÄ k : K, B.equivFun ((algebraMap K L) k) = fun i : Œπ => if i = ‚ü®(1 : L), h1L‚ü© then k else 0 :=
    basis_one hB1 -/
  -- Define a function g : L ‚Üí ‚Ñù by setting g (‚àëki ‚Ä¢ ei) = max·µ¢ ‚Äñ ki ‚Äñ
  set g : L ‚Üí ‚Ñù := B.norm
  -- g 0 = 0
  have hg0 : g 0 = 0 := B.norm_zero'
  -- g takes nonnegative values
  have hg_nonneg : ‚àÄ x : L, 0 ‚â§ g x := fun x => norm_nonneg _
  -- g extends the norm on K
  have hg_ext : FunctionExtends (norm : K ‚Üí ‚Ñù) g := Basis.norm_extends hB1
  -- g is nonarchimedean
  have hg_na : IsNonarchimedean g := Basis.norm_isNonarchimedean hna
  -- g satisfies the triangle inequality
  have hg_add : ‚àÄ a b : L, g (a + b) ‚â§ g a + g b := add_le_of_isNonarchimedean hg_nonneg hg_na
  -- g (-a) = g a
  have hg_neg : ‚àÄ a : L, g (-a) = g a := B.norm_neg
  -- g is multiplicatively bounded
  have hg_bdd : ‚àÉ (c : ‚Ñù) (_ : 0 < c), ‚àÄ x y : L, g (x * y) ‚â§ c * g x * g y :=
    Basis.norm_is_bdd hB1 hna
  -- g is a K-module norm
  have hg_mul : ‚àÄ (k : K) (y : L), g ((algebraMap K L) k * y) = g ((algebraMap K L) k) * g y :=
    fun k y => Basis.norm_smul hB1 k y
  -- Using BGR Prop. 1.2.1/2, we can smooth g to a ring norm f on L that extends the norm on K.
  set f := seminormFromBounded hg0 hg_nonneg hg_bdd hg_add hg_neg
  have hf_na : IsNonarchimedean f := seminorm_from_bounded_isNonarchimedean hg_nonneg hg_bdd hg_na
  have hf_1 : f 1 ‚â§ 1 := seminorm_from_bounded_is_norm_le_one_class hg_nonneg hg_bdd
  have hf_ext : FunctionExtends (norm : K ‚Üí ‚Ñù) f := by
    intro k
    rw [‚Üê hg_ext]
    exact seminorm_from_bounded_of_mul_apply hg_nonneg hg_bdd (hg_mul k)
  -- Using BGR Prop. 1.3.2/1, we obtain from f  a power multiplicative K-algebra norm on L
  -- extending the norm on K.
  set F' := smoothingSeminorm f hf_1 hf_na with hF'
  have hF'_ext : ‚àÄ k : K, F' ((algebraMap K L) k) = ‚Äñk‚Äñ := by
    intro k
    rw [‚Üê hf_ext _]
    exact smoothingSeminorm_apply_of_is_mul f hf_1 hf_na
      (seminorm_from_bounded_of_mul_is_mul hg_nonneg hg_bdd (hg_mul k))
  have hF'_1 : F' 1 = 1 := by
    have h1 : (1 : L) = (algebraMap K L) 1 := by rw [map_one]
    simp only [h1, hF'_ext (1 : K), norm_one]
  have hF'_0 : F' ‚â† 0 := DFunLike.ne_iff.mpr ‚ü®(1 : L), by rw [hF'_1]; exact one_ne_zero‚ü©
  set F : AlgebraNorm K L :=
    { RingSeminorm.toRingNorm F' hF'_0 with
      smul' := fun k y => by
        have hk : ‚àÄ y : L, f (algebraMap K L k * y) = f (algebraMap K L k) * f y :=
          seminorm_from_bounded_of_mul_is_mul hg_nonneg hg_bdd (hg_mul k)
        have hfk : ‚Äñk‚Äñ = (smoothingSeminorm f hf_1 hf_na) ((algebraMap K L) k) := by
          rw [‚Üê hf_ext k, eq_comm, smoothingSeminorm_apply_of_is_mul f hf_1 hf_na hk]
        simp only [hfk, hF']
        erw [‚Üê smoothingSeminorm_of_mul f hf_1 hf_na hk y, Algebra.smul_def]
        rfl }
  have hF_ext : ‚àÄ k : K, F ((algebraMap K L) k) = ‚Äñk‚Äñ := by
    intro k
    rw [‚Üê hf_ext _]
    exact smoothingSeminorm_apply_of_is_mul f hf_1 hf_na
      (seminorm_from_bounded_of_mul_is_mul hg_nonneg hg_bdd (hg_mul k))
  exact ‚ü®F, smoothing_seminorm_isPowMul f hf_1, hF_ext,
      smoothing_seminorm_isNonarchimedean f hf_1 hf_na‚ü©
