/-
Copyright (c) 2023 Mar√≠a In√©s de Frutos-Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mar√≠a In√©s de Frutos-Fern√°ndez
-/
import LocalClassFieldTheory.FromMathlib.RingSeminorm
import Mathlib.Analysis.SpecialFunctions.Pow.Continuity
import Mathlib.Algebra.Order.Hom.Basic

#align_import from_mathlib.pow_mult_faithful

/-!
# Equivalent power-multiplicative norms

In this file, we prove [BGR, Proposition 3.1.5/1]: if `R` is a normed commutative ring and `f‚ÇÅ` and
`f‚ÇÇ` are two power-multiplicative `R`-algebra norms on `S`, then if `f‚ÇÅ` and `f‚ÇÇ` are equivalent on
every subring `R[y]` for `y : S`, it follows that `f‚ÇÅ = f‚ÇÇ`.

## Main Definitions
* `algebra_norm.restriction` : The restriction of an algebra norm to a subalgebra.
* `ring_hom.is_bounded_wrt` :A ring homomorphism `f : Œ± ‚Üí+* Œ≤` is bounded with respect to the
  functions `nŒ± : Œ± ‚Üí ‚Ñù` and `nŒ≤ : Œ≤ ‚Üí ‚Ñù` if there exists a positive constant `C` such that for all
  `x` in `Œ±`, `nŒ≤ (f x) ‚â§ C * nŒ± x`.

## Main Results
* `eq_of_pow_mult_faithful` : the proof of [BGR, Proposition 3.1.5/1].

## References
* [S. Bosch, U. G√ºntzer, R. Remmert, *Non-Archimedean Analysis*][bosch-guntzer-remmert]

## Tags

norm, equivalent, power-multiplicative
-/


open scoped Topology

/-- A homomorphism `f` between semi_normed_rings is bounded if there exists a positive
  constant `C` such that for all `x` in `Œ±`, `norm (f x) ‚â§ C * norm x`. -/
def RingHom.IsBounded {Œ± : Type _} [SeminormedRing Œ±] {Œ≤ : Type _} [SeminormedRing Œ≤]
    (f : Œ± ‚Üí+* Œ≤) : Prop :=
  ‚àÉ C : ‚Ñù, 0 < C ‚àß ‚àÄ x : Œ±, norm (f x) ‚â§ C * norm x

/-- A ring homomorphism `f : Œ± ‚Üí+* Œ≤` is bounded with respect to the functions `nŒ± : Œ± ‚Üí ‚Ñù` and
  `nŒ≤ : Œ≤ ‚Üí ‚Ñù` if there exists a positive constant `C` such that for all `x` in `Œ±`,
  `nŒ≤ (f x) ‚â§ C * nŒ± x`. -/
def RingHom.IsBoundedWrt {Œ± : Type _} [Ring Œ±] {Œ≤ : Type _} [Ring Œ≤] (nŒ± : Œ± ‚Üí ‚Ñù) (nŒ≤ : Œ≤ ‚Üí ‚Ñù)
    (f : Œ± ‚Üí+* Œ≤) : Prop :=
  ‚àÉ C : ‚Ñù, 0 < C ‚àß ‚àÄ x : Œ±, nŒ≤ (f x) ‚â§ C * nŒ± x

/-- If `f : Œ± ‚Üí+* Œ≤` is bounded with respect to a ring seminorm `nŒ±` on `Œ±` and a
  power-multiplicative function `nŒ≤ : Œ≤ ‚Üí ‚Ñù`, then `‚àÄ x : Œ±, nŒ≤ (f x) ‚â§ nŒ± x`. -/
theorem contraction_of_is_pm_wrt {F : Type _} {Œ± : outParam (Type _)} [Ring Œ±]
    [RingSeminormClass F Œ± ‚Ñù] {Œ≤ : Type _} [Ring Œ≤] (nŒ± : F) {nŒ≤ : Œ≤ ‚Üí ‚Ñù} (hŒ≤ : IsPowMul nŒ≤)
    {f : Œ± ‚Üí+* Œ≤} (hf : f.IsBoundedWrt nŒ± nŒ≤) (x : Œ±) : nŒ≤ (f x) ‚â§ nŒ± x :=
  by
  obtain ‚ü®C, hC0, hC‚ü© := hf
  have hlim : Filter.Tendsto (fun n : ‚Ñï => C ^ (1 / (n : ‚Ñù)) * nŒ± x) Filter.atTop (ùìù (nŒ± x)) :=
    by
    have : ùìù (nŒ± x) = ùìù (1 * nŒ± x) := by rw [one_mul]
    rw [this]
    apply Filter.Tendsto.mul
    ¬∑ apply Filter.Tendsto.comp _ (tendsto_const_div_atTop_nhds_0_nat 1)
      rw [‚Üê Real.rpow_zero C]
      apply ContinuousAt.tendsto (Real.continuousAt_const_rpow (ne_of_gt hC0))
    exact tendsto_const_nhds
  apply ge_of_tendsto hlim
  simp only [Filter.eventually_atTop, ge_iff_le]
  use 1
  intro n hn
  have h : (C ^ (1 / n : ‚Ñù)) ^ n = C := by
    have hn0 : (n : ‚Ñù) ‚â† 0 := Nat.cast_ne_zero.mpr (ne_of_gt hn)
    rw [‚Üê Real.rpow_nat_cast, ‚Üê Real.rpow_mul (le_of_lt hC0), one_div, inv_mul_cancel hn0,
      Real.rpow_one]
  apply le_of_pow_le_pow_left (ne_of_gt hn)
    (mul_nonneg (Real.rpow_nonneg_of_nonneg (le_of_lt hC0) _) (map_nonneg _ _))
  ¬∑ rw [mul_pow, h, ‚Üê hŒ≤ _ hn, ‚Üê RingHom.map_pow]
    apply le_trans (hC (x ^ n))
    rw [mul_le_mul_left hC0]
    exact map_pow_le_pow _ _ (Nat.one_le_iff_ne_zero.mp hn)

/-- Given a bounded `f : Œ± ‚Üí+* Œ≤` between seminormed rings, is the seminorm on `Œ≤` is
  power-multiplicative, then `f` is a contraction. -/
theorem contraction_of_is_pm {Œ± : Type _} [SeminormedRing Œ±] {Œ≤ : Type _} [SeminormedRing Œ≤]
    (hŒ≤ : IsPowMul (norm : Œ≤ ‚Üí ‚Ñù)) {f : Œ± ‚Üí+* Œ≤} (hf : f.IsBounded) (x : Œ±) : norm (f x) ‚â§ norm x :=
  contraction_of_is_pm_wrt (SeminormedRing.toRingSeminorm Œ±) hŒ≤ hf x

/-- Given two power-multiplicative ring seminorms `f, g` on `Œ±`, if `f` is bounded by a positive
  multiple of `g` and viceversa, then `f = g`. -/
theorem eq_seminorms {F : Type _} {Œ± : outParam (Type _)} [Ring Œ±] [RingSeminormClass F Œ± ‚Ñù]
    (f g : F) (hfpm : IsPowMul f) (hgpm : IsPowMul g)
    (hfg : ‚àÉ (r : ‚Ñù) (_ : 0 < r), ‚àÄ a : Œ±, f a ‚â§ r * g a)
    (hgf : ‚àÉ (r : ‚Ñù) (_ : 0 < r), ‚àÄ a : Œ±, g a ‚â§ r * f a) : f = g := by
  obtain ‚ü®r, hr0, hr‚ü© := hfg
  obtain ‚ü®s, hs0, hs‚ü© := hgf
  have hle : RingHom.IsBoundedWrt f g (RingHom.id _) := ‚ü®s, hs0, hs‚ü©
  have hge : RingHom.IsBoundedWrt g f (RingHom.id _) := ‚ü®r, hr0, hr‚ü©
  rw [‚Üê Function.Injective.eq_iff FunLike.coe_injective']
  ext x
  exact le_antisymm (contraction_of_is_pm_wrt g hfpm hge x) (contraction_of_is_pm_wrt f hgpm hle x)

variable {R S : Type _} [NormedCommRing R] [CommRing S] [Algebra R S]

/-- The restriction of a power-multiplicative function to a subalgebra is power-multiplicative. -/
theorem IsPowMul.restriction (A : Subalgebra R S) {f : S ‚Üí ‚Ñù} (hf_pm : IsPowMul f) :
    IsPowMul fun x : A => f x.val := fun x n hn => by
  simpa [SubsemiringClass.coe_pow] using hf_pm (‚Üëx) hn

/-- The restriction of an algebra norm to a subalgebra. -/
def AlgebraNorm.restriction (A : Subalgebra R S) (f : AlgebraNorm R S) : AlgebraNorm R A where
  toFun       := fun x : A => f x.val
  map_zero'   := map_zero f
  add_le' x y := map_add_le_add _ _ _
  neg' x      := map_neg_eq_map _ _
  mul_le' x y := map_mul_le_mul _ _ _
  eq_zero_of_map_eq_zero' x hx := by
    rw [‚Üê ZeroMemClass.coe_eq_zero]; exact eq_zero_of_map_eq_zero f hx
  smul' r x := map_smul_eq_mul _ _ _

/-- If `R` is a normed commutative ring and `f‚ÇÅ` and `f‚ÇÇ` are two power-multiplicative `R`-algebra
  norms on `S`, then if `f‚ÇÅ` and `f‚ÇÇ` are equivalent on every  subring `R[y]` for `y : S`, it
  follows that `f‚ÇÅ = f‚ÇÇ` [BGR, Proposition 3.1.5/1].  -/
theorem eq_of_pow_mult_faithful (f‚ÇÅ : AlgebraNorm R S) (hf‚ÇÅ_pm : IsPowMul f‚ÇÅ) (f‚ÇÇ : AlgebraNorm R S)
    (hf‚ÇÇ_pm : IsPowMul f‚ÇÇ)
    (h_eq : ‚àÄ y : S, ‚àÉ (C‚ÇÅ C‚ÇÇ : ‚Ñù) (_ : 0 < C‚ÇÅ) (_ : 0 < C‚ÇÇ),
      ‚àÄ x : Algebra.adjoin R {y}, f‚ÇÅ x.val ‚â§ C‚ÇÅ * f‚ÇÇ x.val ‚àß f‚ÇÇ x.val ‚â§ C‚ÇÇ * f‚ÇÅ x.val) :
    f‚ÇÅ = f‚ÇÇ := by
  ext x
  set g‚ÇÅ : AlgebraNorm R (Algebra.adjoin R ({x} : Set S)) := AlgebraNorm.restriction _ f‚ÇÅ
  set g‚ÇÇ : AlgebraNorm R (Algebra.adjoin R ({x} : Set S)) := AlgebraNorm.restriction _ f‚ÇÇ
  have hg‚ÇÅ_pm : IsPowMul g‚ÇÅ := IsPowMul.restriction _ hf‚ÇÅ_pm
  have hg‚ÇÇ_pm : IsPowMul g‚ÇÇ := IsPowMul.restriction _ hf‚ÇÇ_pm
  let y : Algebra.adjoin R ({x} : Set S) := ‚ü®x, Algebra.self_mem_adjoin_singleton R x‚ü©
  have hy : x = y.val := rfl
  have h1 : f‚ÇÅ y.val = g‚ÇÅ y := rfl
  have h2 : f‚ÇÇ y.val = g‚ÇÇ y := rfl
  obtain ‚ü®C‚ÇÅ, C‚ÇÇ, hC‚ÇÅ_pos, hC‚ÇÇ_pos, hC‚ü© := h_eq x
  obtain ‚ü®hC‚ÇÅ, hC‚ÇÇ‚ü© := forall_and.mp hC
  rw [hy, h1, h2, eq_seminorms g‚ÇÅ g‚ÇÇ hg‚ÇÅ_pm hg‚ÇÇ_pm ‚ü®C‚ÇÅ, hC‚ÇÅ_pos, hC‚ÇÅ‚ü© ‚ü®C‚ÇÇ, hC‚ÇÇ_pos, hC‚ÇÇ‚ü©]
