/-
Copyright (c) 2024 Mar√≠a In√©s de Frutos-Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mar√≠a In√©s de Frutos-Fern√°ndez
-/
--import Mathlib.Analysis.Normed.Ring.Seminorm
import Mathlib.Analysis.SpecialFunctions.Pow.Continuity
import Mathlib.Topology.MetricSpace.Sequences
import LocalClassFieldTheory.FromMathlib.Limsup
import LocalClassFieldTheory.FromMathlib.RingSeminorm -- add lemmas to this PR

/-!
# smoothingSeminorm
In this file, we prove [BGR, Proposition 1.3.2/1][bosch-guntzer-remmert] : if `f` is a
nonarchimedean seminorm on `R`, then `iInf (Œª (n : pnat), (f(x^(n : ‚Ñï)))^(1/(n : ‚Ñù)))` is a
power-multiplicative nonarchimedean seminorm on `R`.

## Main Definitions

* `smoothingSeminorm_seq` : the `‚Ñù`-valued sequence sending `n` to `(f (x ^ n))^(1/n : ‚Ñù)`.
* `smoothingSeminorm'` : the iInf of the sequence `f(x^(n : ‚Ñï)))^(1/(n : ‚Ñù)`.
* `smoothingSeminorm` : if `f 1 ‚â§ 1` and `f` is nonarchimedean, then `smoothingSeminorm'`
  is a ring seminorm.

## Main Results

* `smoothingSeminorm'_isLimit` :if `f 1 ‚â§ 1`, then `smoothingSeminorm' f x` is the limit
  of `smoothingSeminorm_seq f x` as `n` tends to infinity.
* `smoothingSeminorm_isNonarchimedean` : if `f 1 ‚â§ 1` and `f` is nonarchimedean, then
  `smoothingSeminorm'` is nonarchimedean.
* `smoothingSeminorm_isPowMul` : if `f 1 ‚â§ 1` and `f` is nonarchimedean, then
  `smoothingSeminorm' f` is power-multiplicative.

## References
* [S. Bosch, U. G√ºntzer, R. Remmert, *Non-Archimedean Analysis*][bosch-guntzer-remmert]

## Tags

smoothingSeminorm, seminorm, nonarchimedean
-/

-- In PR #15373

noncomputable section

open Real

open scoped Topology NNReal

-- In this section we prove auxiliary lemmas, which will be PR'd to the appropriate mathlib files.
section AuxLemmas

/-- If `a` belongs to the interval `[0, b]`, then so does `b - a`. -/
theorem sub_mem_Icc {a b : ‚Ñù} (h : a ‚àà Set.Icc (0 : ‚Ñù) b) : b - a ‚àà Set.Icc (0 : ‚Ñù) b := by
  rw [Set.mem_Icc] at h ‚ä¢
  rw [sub_le_self_iff]
  exact ‚ü®sub_nonneg_of_le h.2, h.1‚ü©

/-- If `x` is multiplicative with respect to `f`, then so is any `x^n`. -/
theorem is_mul_pow_of_is_mul {R : Type _} [CommRing R] (f : R ‚Üí ‚Ñù) {x : R}
    (hx : ‚àÄ y : R, f (x * y) = f x * f y) : ‚àÄ (n : ‚Ñï) (y : R), f (x ^ n * y) = f x ^ n * f y := by
  intro n
  induction' n with n hn
  ¬∑ intro y; rw [pow_zero, pow_zero, one_mul, one_mul]
  ¬∑ intro y; rw [pow_succ', pow_succ', mul_assoc, mul_assoc, hx, hn]

-- Not used in this file
/-- For any `r : ‚Ñù‚â•0` and any positive `n : ‚Ñï`,  `(r ^ n)^(1/n : ‚Ñù) = r`. -/
theorem NNReal.pow_n_n_inv (r : ‚Ñù‚â•0) {n : ‚Ñï} (hn : 0 < n) : (r ^ n) ^ (1 / n : ‚Ñù) = r := by
  conv_rhs => rw [‚Üê rpow_one r, ‚Üê mul_one_div_cancel
    (Nat.cast_ne_zero.mpr (Nat.one_le_iff_ne_zero.mp hn))]
  rw [rpow_mul, rpow_natCast]

-- Not used in this file
/-- For any nonnegative `r : ‚Ñù` and any positive `n : ‚Ñï`,  `(r ^ n)^(1/n : ‚Ñù) = r`. -/
theorem Real.pow_n_n_inv {r : ‚Ñù} (hr : 0 ‚â§ r) {n : ‚Ñï} (hn : 0 < n) : (r ^ n) ^ (1 / n : ‚Ñù) = r := by
  conv_rhs => rw [‚Üê rpow_one r, ‚Üê mul_one_div_cancel
    (Nat.cast_ne_zero.mpr (Nat.one_le_iff_ne_zero.mp hn))]
  rw [Real.rpow_mul hr, rpow_natCast]

open Nat
namespace Filter

/-- If there exists real constants `b`and `B` such that for `n` big enough, `b ‚â§ f n ‚â§ B`, then
  `f n / (n : ‚Ñù)` tends to `0` as `n` tends to infinity. -/
theorem tendsto_bdd_div_atTop_nhds_zero_nat (f : ‚Ñï ‚Üí ‚Ñù) (b : ‚Ñù)
    (hb : ‚àÄ·∂† n : ‚Ñï in atTop, b ‚â§ f n) (B : ‚Ñù) (hB : ‚àÄ·∂† n : ‚Ñï in atTop, f n ‚â§ B) :
    Tendsto (fun n : ‚Ñï => f n / (n : ‚Ñù)) atTop (ùìù 0) := by
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le' (tendsto_const_div_atTop_nhds_zero_nat b)
      (tendsto_const_div_atTop_nhds_zero_nat B) ?_ ?_
  ¬∑ simp only [eventually_atTop, ge_iff_le] at hb ‚ä¢
    obtain ‚ü®N, hN‚ü© := hb
    exact ‚ü®N, fun n hn ‚Ü¶ div_le_div_of_nonneg_right (hN n hn) (cast_nonneg _)‚ü©
  ¬∑ simp only [eventually_atTop, ge_iff_le] at hB ‚ä¢
    obtain ‚ü®N, hN‚ü© := hB
    exact ‚ü®N, fun n hn ‚Ü¶ div_le_div_of_nonneg_right (hN n hn) (cast_nonneg _)‚ü©

/-- For any positive `m : ‚Ñï`, `((n % m : ‚Ñï) : ‚Ñù) / (n : ‚Ñù)` tends to `0` as `n` tends to `‚àû`. -/
theorem tendsto_mod_div_atTop_nhds_zero_nat {m : ‚Ñï} (hm : 0 < m) :
    Tendsto (fun n : ‚Ñï => ((n % m : ‚Ñï) : ‚Ñù) / (n : ‚Ñù)) atTop (ùìù 0) := by
  apply tendsto_bdd_div_atTop_nhds_zero_nat (fun n : ‚Ñï => ((n % m : ‚Ñï) : ‚Ñù)) 0
    (Eventually.of_forall (fun _ ‚Ü¶ cast_nonneg _)) m
  apply Eventually.of_forall
  intro n
  simp only [cast_le, le_of_lt (mod_lt n hm)]

/-- If `u` tends to `‚àû` as `n` tends to `‚àû`, then for `n` big enough
  `((s n : ‚Ñù) / (u n : ‚Ñù)) * (u n : ‚Ñù) = (s n : ‚Ñù)` holds. -/
theorem div_mul_eventually_cancel (s : ‚Ñï ‚Üí ‚Ñï) {u : ‚Ñï ‚Üí ‚Ñï} (hu : Tendsto u atTop atTop) :
    (fun n => (s n : ‚Ñù) / (u n : ‚Ñù) * (u n : ‚Ñù)) =·∂†[atTop] fun n => (s n : ‚Ñù) := by
  simp only [EventuallyEq, eventually_atTop, ge_iff_le]
  simp only [tendsto_atTop, eventually_atTop, ge_iff_le] at hu
  obtain ‚ü®n, hn‚ü© := hu 1
  use n
  intro m hm
  rw [div_mul_cancel‚ÇÄ (s m : ‚Ñù) (cast_ne_zero.mpr (one_le_iff_ne_zero.mp (hn m hm)))]

/-- If when `n` tends to `‚àû`, `u` tends to `‚àû` and `(s n : ‚Ñù) / (u n : ‚Ñù))` tends to a nonzero
  constant, then `s` tends to `‚àû`. -/
theorem Tendsto.num {s u : ‚Ñï ‚Üí ‚Ñï} (hu : Tendsto u atTop atTop) {a : ‚Ñù} (ha : 0 < a)
    (hlim : Tendsto (fun n : ‚Ñï => (s n : ‚Ñù) / (u n : ‚Ñù)) atTop (ùìù a)) : Tendsto s atTop atTop :=
  tendsto_natCast_atTop_iff.mp (Tendsto.congr' (div_mul_eventually_cancel s hu)
    (Tendsto.mul_atTop ha hlim (tendsto_natCast_atTop_iff.mpr hu)))

/-- If `f` is a ring seminorm on `R` with `f 1 ‚â§ 1` and `s : ‚Ñï ‚Üí ‚Ñï` is bounded by `n`, then
  `f (x ^ s (œà n)) ^ (1 / (œà n : ‚Ñù))` is eventually bounded. -/
theorem isBoundedUnder {R : Type _} [CommRing R] (f : RingSeminorm R) (hf1 : f 1 ‚â§ 1) {s : ‚Ñï ‚Üí ‚Ñï}
    (hs_le : ‚àÄ n : ‚Ñï, s n ‚â§ n) {x : R} (œà : ‚Ñï ‚Üí ‚Ñï) :
    IsBoundedUnder LE.le atTop fun n : ‚Ñï => f (x ^ s (œà n)) ^ (1 / (œà n : ‚Ñù)) := by
  have h_le : ‚àÄ m : ‚Ñï, f (x ^ s (œà m)) ^ (1 / (œà m : ‚Ñù)) ‚â§ f x ^ ((s (œà m) : ‚Ñù) / (œà m : ‚Ñù)) := by
    intro m
    rw [‚Üê mul_one_div (s (œà m) : ‚Ñù), rpow_mul (apply_nonneg f x), rpow_natCast]
    exact rpow_le_rpow (apply_nonneg _ _) (map_pow_le_pow' hf1 x _)
      (one_div_nonneg.mpr (cast_nonneg _))
  apply isBoundedUnder_of
  by_cases hfx : f x ‚â§ 1
  ¬∑ use 1, fun m => le_trans (h_le m)
      (rpow_le_one (apply_nonneg _ _) hfx (div_nonneg (cast_nonneg _) (cast_nonneg _)))
  ¬∑ use f x
    intro m
    apply le_trans (h_le m)
    conv_rhs => rw [‚Üê rpow_one (f x)]
    exact rpow_le_rpow_of_exponent_le (le_of_lt (not_le.mp hfx))
      (div_le_one_of_le (cast_le.mpr (hs_le _)) (cast_nonneg _))

end Filter

end AuxLemmas

open Filter Nat Real

variable {R : Type _} [CommRing R] (f : RingSeminorm R)

section smoothingSeminorm

/-- The `‚Ñù`-valued sequence sending `n` to `(f (x ^ n))^(1/n : ‚Ñù)`. -/
def smoothingSeminormSeq (x : R) : ‚Ñï ‚Üí ‚Ñù := fun n => f (x ^ n) ^ (1 / n : ‚Ñù)

/-- For any positive `Œµ`, there exists a positive natural number `m` such that
  `f (x ^ (m : ‚Ñï)) ^ (1 / m : ‚Ñù) < iInf (Œª (n : pnat), (f(x ^(n : ‚Ñï)))^(1/(n : ‚Ñù))) + Œµ/2`. -/
private theorem smoothingSeminorm_seq_hasLimit_m (x : R) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    ‚àÉ m : PNat, f (x ^ (m : ‚Ñï)) ^ (1 / m : ‚Ñù) <
        (iInf fun n : PNat => f (x ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù))) + Œµ / 2 :=
  exists_lt_of_ciInf_lt (lt_add_of_le_of_pos (le_refl _) (half_pos hŒµ))

private theorem smoothingSeminorm_seq_hasLimit_aux {L : ‚Ñù} (hL : 0 ‚â§ L) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ)
    {m1 : ‚Ñï} (hm1 : 0 < m1) {x : R} (hx : f x ‚â† 0) :
    Tendsto
      (fun n : ‚Ñï => (L + Œµ) ^ (-(((n % m1 : ‚Ñï) : ‚Ñù) / (n : ‚Ñù))) * (f x ^ (n % m1)) ^ (1 / (n : ‚Ñù)))
      atTop (ùìù 1) := by
  rw [‚Üê mul_one (1 : ‚Ñù)]
  have h_exp : Tendsto (fun n : ‚Ñï => ((n % m1 : ‚Ñï) : ‚Ñù) / (n : ‚Ñù)) atTop (ùìù 0) :=
    tendsto_mod_div_atTop_nhds_zero_nat hm1
  apply Tendsto.mul
  ¬∑ have h0 : Tendsto (fun t : ‚Ñï => -(((t % m1 : ‚Ñï) : ‚Ñù) / (t : ‚Ñù))) atTop (ùìù 0) := by
      rw [‚Üê neg_zero]
      exact Tendsto.neg h_exp
    rw [‚Üê rpow_zero (L + Œµ)]
    apply Tendsto.rpow tendsto_const_nhds h0
    rw [ne_eq, add_eq_zero_iff_of_nonneg hL (le_of_lt hŒµ)]
    exact Or.inl (not_and_of_not_right _ (ne_of_gt hŒµ))
  ¬∑ simp_rw [mul_one, ‚Üê rpow_natCast, ‚Üê rpow_mul (apply_nonneg f x), ‚Üê mul_div_assoc, mul_one, ‚Üê
      rpow_zero (f x)]
    exact Tendsto.rpow tendsto_const_nhds h_exp (Or.inl hx)

/-- `smoothingSeminorm_seq` is bounded below by zero. -/
theorem smoothingSeminorm_seq_bddBelow (x : R) :
    BddBelow (Set.range fun n : ‚Ñï+ => f (x ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù))) := by
  use 0
  rintro y ‚ü®n, rfl‚ü©
  exact rpow_nonneg (apply_nonneg f _) _

/-- The iInf of the sequence `f(x^(n : ‚Ñï)))^(1/(n : ‚Ñù)`. -/
def smoothingSeminorm' (x : R) : ‚Ñù :=
  iInf fun n : PNat => f (x ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù))

/-- If `f x = 0`, then `smoothingSeminorm' f x` is the limit of `smoothingSeminorm_seq f x`. -/
theorem smoothingSeminorm'_isLimit_zero {x : R} (hx : f x = 0) :
    Tendsto (smoothingSeminormSeq f x) atTop (ùìù (smoothingSeminorm' f x)) := by
  have h0 : ‚àÄ (n : ‚Ñï) (_hn : 1 ‚â§ n), f (x ^ n) ^ (1 / (n : ‚Ñù)) = 0 := fun n hn ‚Ü¶ by
    have hfn : f (x ^ n) = 0 := by
      apply le_antisymm _ (apply_nonneg f _)
      rw [‚Üê zero_pow (pos_iff_ne_zero.mp hn), ‚Üê hx]
      exact map_pow_le_pow _ x (one_le_iff_ne_zero.mp hn)
    rw [hfn, zero_rpow (one_div_cast_ne_zero (one_le_iff_ne_zero.mp hn))]
  have hL0 : (iInf fun n : PNat => f (x ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù))) = 0 :=
    le_antisymm
      (ciInf_le_of_le (smoothingSeminorm_seq_bddBelow f x) (1 : PNat) (le_of_eq (h0 1 (le_refl _))))
      (le_ciInf fun n => rpow_nonneg (apply_nonneg f _) _)
  simp only [hL0, smoothingSeminormSeq, smoothingSeminorm']
  exact tendsto_atTop_of_eventually_const h0

/-- If `f 1 ‚â§ 1` and `f x ‚â†  0`, then `smoothingSeminorm' f x` is the limit of
`smoothingSeminorm_seq f x`. -/
theorem smoothingSeminorm'_isLimit_ne_zero (hf1 : f 1 ‚â§ 1) {x : R} (hx : f x ‚â† 0) :
    Tendsto (smoothingSeminormSeq f x) atTop (ùìù (smoothingSeminorm' f x)) := by
  simp only [smoothingSeminorm']
  set L := iInf fun n : PNat => f (x ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù))
  have hL0 : 0 ‚â§ L := le_ciInf fun x => rpow_nonneg (apply_nonneg _ _) _
  rw [Metric.tendsto_atTop]
  intro Œµ hŒµ
  obtain ‚ü®m1, hm1‚ü© := smoothingSeminorm_seq_hasLimit_m f x hŒµ
  obtain ‚ü®m2, hm2‚ü© : ‚àÉ m : ‚Ñï, ‚àÄ n ‚â• m,
      (L + Œµ / 2) ^ (-(((n % m1 : ‚Ñï) : ‚Ñù) / (n : ‚Ñù))) * (f x ^ (n % m1)) ^ (1 / (n : ‚Ñù)) - 1 ‚â§
      Œµ / (2 * (L + Œµ / 2)) := by
    have hŒµ2 : 0 < Œµ / 2 := half_pos hŒµ
    have hL2 := smoothingSeminorm_seq_hasLimit_aux f hL0 hŒµ2 (PNat.pos m1) hx
    rw [Metric.tendsto_atTop] at hL2
    set Œ¥ : ‚Ñù := Œµ / (2 * (L + Œµ / 2)) with hŒ¥_def
    have hŒ¥ : 0 < Œ¥ := by
      rw [hŒ¥_def, div_mul_eq_div_mul_one_div]
      exact mul_pos hŒµ2
        ((one_div (L + Œµ / 2)).symm ‚ñ∏ inv_pos_of_pos (add_pos_of_nonneg_of_pos hL0 hŒµ2))
    obtain ‚ü®N, hN‚ü© := hL2 Œ¥ hŒ¥
    use N
    intro n hn
    specialize hN n hn
    rw [Real.dist_eq, abs_lt] at hN
    exact le_of_lt hN.right
  use max (m1 : ‚Ñï) m2
  intro n hn
  have hn0 : 0 < n := lt_of_lt_of_le (lt_of_lt_of_le (PNat.pos m1) (le_max_left (m1 : ‚Ñï) m2)) hn
  rw [Real.dist_eq, abs_lt]
  have hL_le : L ‚â§ smoothingSeminormSeq f x n := by
    simp only [smoothingSeminormSeq]
    rw [‚Üê PNat.mk_coe n hn0]
    apply ciInf_le (smoothingSeminorm_seq_bddBelow f x)
  refine ‚ü®lt_of_lt_of_le (neg_lt_zero.mpr hŒµ) (sub_nonneg.mpr hL_le), ?_‚ü©
  ¬∑ suffices h : smoothingSeminormSeq f x n < L + Œµ by
      rw [tsub_lt_iff_left hL_le]
      exact h
    by_cases hxn : f (x ^ (n % m1)) = 0
    ¬∑ simp only [smoothingSeminormSeq]
      nth_rw 1 [‚Üê div_add_mod n m1]
      have hLŒµ : 0 < L + Œµ := add_pos_of_nonneg_of_pos hL0 hŒµ
      apply lt_of_le_of_lt _ hLŒµ
      rw [pow_add, ‚Üê MulZeroClass.mul_zero (f (x ^ ((m1 : ‚Ñï) * (n / (m1 : ‚Ñï)))) ^ (1 / (n : ‚Ñù))),
        ‚Üê zero_rpow (one_div_cast_ne_zero (pos_iff_ne_zero.mp hn0)), ‚Üê hxn,
        ‚Üê mul_rpow (apply_nonneg f _) (apply_nonneg f _)]
      exact rpow_le_rpow (apply_nonneg f _) (map_mul_le_mul f _ _) (one_div_cast_nonneg _)
    ¬∑ have hxn' : 0 < f (x ^ (n % ‚Üëm1)) := lt_of_le_of_ne (apply_nonneg _ _) (Ne.symm hxn)
      simp only [smoothingSeminormSeq]
      nth_rw 1 [‚Üê div_add_mod n m1]
      have h : f (x ^ ((m1 : ‚Ñï) * (n / (m1 : ‚Ñï)))) ^ (1 / (n : ‚Ñù)) ‚â§
          (f (x ^ (m1 : ‚Ñï)) ^ (n / (m1 : ‚Ñï))) ^ (1 / (n : ‚Ñù)) := by
        apply rpow_le_rpow (apply_nonneg f _) _ (one_div_cast_nonneg _)
        rw [pow_mul]
        exact map_pow_le_pow f (x^(m1 : ‚Ñï))
          (pos_iff_ne_zero.mp (Nat.div_pos (le_trans (le_max_left (m1 : ‚Ñï) m2) hn) (PNat.pos m1)))
      have hL0' : 0 < L + Œµ / 2 := add_pos_of_nonneg_of_pos hL0 (half_pos hŒµ)
      have h1 : (f (x ^ (m1 : ‚Ñï)) ^ (n / (m1 : ‚Ñï))) ^ (1 / (n : ‚Ñù)) <
          (L + Œµ / 2) * (L + Œµ / 2) ^ (-(((n % m1 : ‚Ñï) : ‚Ñù) / (n : ‚Ñù))) := by
        have hm10 : (m1 : ‚Ñù) ‚â† 0 := cast_ne_zero.mpr (_root_.ne_of_gt (PNat.pos m1))
        rw [‚Üê rpow_lt_rpow_iff (rpow_nonneg (apply_nonneg f _) _) (le_of_lt hL0')
          (cast_pos.mpr (PNat.pos m1)), ‚Üê rpow_mul (apply_nonneg f _), one_div_mul_cancel hm10,
          rpow_one] at hm1
        nth_rw 1 [‚Üê rpow_one (L + Œµ / 2)]
        have : (n : ‚Ñù) / n = (1 : ‚Ñù) := div_self (cast_ne_zero.mpr (_root_.ne_of_gt hn0))
        nth_rw 2 [‚Üê this]; clear this
        nth_rw 3 [‚Üê div_add_mod n m1]
        have h_lt : 0 < ((n / m1 : ‚Ñï) : ‚Ñù) / (n : ‚Ñù) :=
          div_pos (cast_pos.mpr (Nat.div_pos (le_trans (le_max_left _ _) hn) (PNat.pos m1)))
            (cast_pos.mpr hn0)
        rw [‚Üê rpow_natCast, ‚Üê rpow_add hL0', ‚Üê neg_div, div_add_div_same, Nat.cast_add,
          add_neg_cancel_right, Nat.cast_mul, ‚Üê rpow_mul (apply_nonneg f _), mul_one_div,
          mul_div_assoc, rpow_mul (le_of_lt hL0')]
        exact rpow_lt_rpow (apply_nonneg f _) hm1 h_lt
      have h2 : f (x ^ (n % m1)) ^ (1 / (n : ‚Ñù)) ‚â§ (f x ^ (n % m1)) ^ (1 / (n : ‚Ñù)) := by
        by_cases hnm1 : n % m1 = 0
        ¬∑ simpa [hnm1, pow_zero] using rpow_le_rpow (apply_nonneg f _) hf1 (one_div_cast_nonneg _)
        ¬∑ exact rpow_le_rpow (apply_nonneg f _) (map_pow_le_pow f _ hnm1) (one_div_cast_nonneg _)
      have h3 : (L + Œµ / 2) * (L + Œµ / 2) ^ (-(((n % m1 : ‚Ñï) : ‚Ñù) / (n : ‚Ñù))) *
            (f x ^ (n % m1)) ^ (1 / (n : ‚Ñù)) ‚â§ L + Œµ := by
        have heq : L + Œµ = L + Œµ / 2 + Œµ / 2 := by rw [add_assoc, add_halves]
        have hL0' : 0 < L + Œµ / 2 := add_pos_of_nonneg_of_pos hL0 (half_pos hŒµ)
        rw [heq, ‚Üê tsub_le_iff_left]
        nth_rw 3 [‚Üê mul_one (L + Œµ / 2)]
        rw [mul_assoc, ‚Üê mul_sub, mul_comm, ‚Üê le_div_iff‚ÇÄ hL0', div_div]
        exact hm2 n (le_trans (le_max_right (m1 : ‚Ñï) m2) hn)
      have h4 : 0 < f (x ^ (n % ‚Üëm1)) ^ (1 / (n : ‚Ñù)) := rpow_pos_of_pos hxn' _
      have h5 : 0 < (L + Œµ / 2) * (L + Œµ / 2) ^ (-(‚Üë(n % ‚Üëm1) / (n : ‚Ñù))) :=
        mul_pos hL0' (rpow_pos_of_pos hL0' _)
      calc f (x ^ ((m1 : ‚Ñï) * (n / (m1 : ‚Ñï)) + n % m1)) ^ (1 / (n : ‚Ñù)) =
            f (x ^ ((m1 : ‚Ñï) * (n / (m1 : ‚Ñï))) * x ^ (n % m1)) ^ (1 / (n : ‚Ñù)) :=
          by rw [pow_add]
        _ ‚â§ (f (x ^ ((m1 : ‚Ñï) * (n / (m1 : ‚Ñï)))) * f (x ^ (n % m1))) ^ (1 / (n : ‚Ñù)) :=
          (rpow_le_rpow (apply_nonneg f _) (map_mul_le_mul f _ _) (one_div_cast_nonneg _))
        _ = f (x ^ ((m1 : ‚Ñï) * (n / (m1 : ‚Ñï)))) ^ (1 / (n : ‚Ñù)) *
            f (x ^ (n % m1)) ^ (1 / (n : ‚Ñù)) :=
          (mul_rpow (apply_nonneg f _) (apply_nonneg f _))
        _ ‚â§ (f (x ^ (m1 : ‚Ñï)) ^ (n / (m1 : ‚Ñï))) ^ (1 / (n : ‚Ñù)) *
              f (x ^ (n % m1)) ^ (1 / (n : ‚Ñù)) := ((mul_le_mul_right h4).mpr h)
        _ < (L + Œµ / 2) * (L + Œµ / 2) ^ (-(((n % m1 : ‚Ñï) : ‚Ñù) / (n : ‚Ñù))) *
              f (x ^ (n % m1)) ^ (1 / (n : ‚Ñù)) := (mul_lt_mul h1 (le_refl _) h4 (le_of_lt h5))
        _ ‚â§ (L + Œµ / 2) * (L + Œµ / 2) ^ (-(((n % m1 : ‚Ñï) : ‚Ñù) / (n : ‚Ñù))) *
              (f x ^ (n % m1)) ^ (1 / (n : ‚Ñù)) := ((mul_le_mul_left h5).mpr h2)
        _ ‚â§ L + Œµ := h3

/-- If `f 1 ‚â§ 1`, then `smoothingSeminorm' f x` is the limit of `smoothingSeminorm_seq f x`
  as `n` tends to infinity. -/
theorem smoothingSeminorm'_isLimit (hf1 : f 1 ‚â§ 1) (x : R) :
    Tendsto (smoothingSeminormSeq f x) atTop (ùìù (smoothingSeminorm' f x)) := by
  by_cases hx : f x = 0
  ¬∑ exact smoothingSeminorm'_isLimit_zero f hx
  ¬∑ exact smoothingSeminorm'_isLimit_ne_zero f hf1 hx

/-- If `f 1 ‚â§ 1`, then `smoothingSeminorm' f x` is nonnegative. -/
theorem smoothingSeminorm_nonneg (hf1 : f 1 ‚â§ 1) (x : R) : 0 ‚â§ smoothingSeminorm' f x := by
  apply ge_of_tendsto (smoothingSeminorm'_isLimit f hf1 x)
  simp only [eventually_atTop, ge_iff_le]
  exact ‚ü®1, fun _ _ ‚Ü¶ rpow_nonneg (apply_nonneg f _) _‚ü©

/- /-- If `f 1 ‚â§ 1`, then `smoothingSeminorm' f 0 = 0`. -/
theorem smoothingSeminorm_zero (hf1 : f 1 ‚â§ 1) : smoothingSeminorm' f 0 = 0 := by
  apply
    tendsto_nhds_unique_of_eventuallyEq (smoothingSeminorm'_isLimit f hf1 0) tendsto_const_nhds
  simp only [EventuallyEq, eventually_atTop, ge_iff_le]
  use 1
  intro n hn
  simp only [smoothingSeminormSeq]
  rw [zero_pow (pos_iff_ne_zero.mp hn), map_zero, zero_rpow]
  exact one_div_ne_zero (cast_ne_zero.mpr (one_le_iff_ne_zero.mp hn)) -/

/- /-- If `f (-x) = f x`, the same holds for `smoothingSeminorm' f`. -/
theorem smoothingSeminorm_neg (f_neg : ‚àÄ x : R, f (-x) = f x) (x : R) :
    smoothingSeminorm' f (-x) = smoothingSeminorm' f x := by
  simp only [smoothingSeminorm', smoothingSeminorm']
  congr
  ext n
  rw [neg_pow]
  rcases neg_one_pow_eq_or R n with hpos | hneg
  ¬∑ rw [hpos, one_mul]
  ¬∑ rw [hneg, neg_one_mul, f_neg] -/

/- /-- If `f 1 ‚â§ 1`, then `smoothingSeminorm' f` is submultiplicative. -/
theorem smoothingSeminorm_mul (hf1 : f 1 ‚â§ 1) (x y : R) :
    smoothingSeminorm' f (x * y) ‚â§ smoothingSeminorm' f x * smoothingSeminorm' f y := by
  apply le_of_tendsto_of_tendsto' (smoothingSeminorm'_isLimit f hf1 (x * y))
    (Tendsto.mul (smoothingSeminorm'_isLimit f hf1 x) (smoothingSeminorm'_isLimit f hf1 y))
  intro n
  have hn : 0 ‚â§ 1 / (n : ‚Ñù) := by simp only [one_div, inv_nonneg, cast_nonneg]
  simp only [smoothingSeminormSeq]
  rw [‚Üê mul_rpow (apply_nonneg f _) (apply_nonneg f _), mul_pow]
  exact rpow_le_rpow (apply_nonneg f _) (map_mul_le_mul f _ _) hn -/

/-- If `f 1 ‚â§ 1`, then `smoothingSeminorm' f 1 ‚â§ 1`. -/
theorem smoothingSeminorm_one (hf1 : f 1 ‚â§ 1) : smoothingSeminorm' f 1 ‚â§ 1 := by
  apply le_of_tendsto (smoothingSeminorm'_isLimit f hf1 (1 : R))
  simp only [eventually_atTop, ge_iff_le]
  use 1
  rintro n hn
  simp only [smoothingSeminormSeq]
  rw [one_pow]
  conv_rhs => rw [‚Üê one_rpow (1 / n : ‚Ñù)]
  have hn1 : 0 < (1 / n : ‚Ñù) := by
    apply div_pos zero_lt_one
    rw [‚Üê cast_zero, cast_lt]
    exact succ_le_iff.mp hn
  exact (rpow_le_rpow_iff (apply_nonneg f _) zero_le_one hn1).mpr hf1

/-- For any `x` and any positive `n`, `smoothingSeminorm' f x  ‚â§ f (x^(n : ‚Ñï))^(1/n : ‚Ñù)`. -/
theorem smoothingSeminorm_le_term (x : R) (n : PNat) :
    smoothingSeminorm' f x ‚â§ f (x ^ (n : ‚Ñï)) ^ (1 / n : ‚Ñù) :=
  ciInf_le (smoothingSeminorm_seq_bddBelow f x) _

/-- For all `x : R`, `smoothingSeminorm' f x ‚â§ f x`. -/
theorem smoothingSeminorm_le (x : R) : smoothingSeminorm' f x ‚â§ f x := by
  convert smoothingSeminorm_le_term f x 1
  rw [PNat.one_coe, pow_one, cast_one, div_one, rpow_one]


/- In this section, we prove that if `f` is nonarchimedean, then `smoothingSeminorm' f` is
  nonarchimedean. -/
section IsNonarchimedean

theorem exists_index_le (hna : IsNonarchimedean f) (x y : R) (n : ‚Ñï) :
    ‚àÉ (m : ‚Ñï) (_ : m ‚àà Finset.range (n + 1)), f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§
      (f (x ^ m) * f (y ^ (n - m : ‚Ñï))) ^ (1 / (n : ‚Ñù)) := by
  obtain ‚ü®m, hm_lt, hm‚ü© := isNonarchimedean_add_pow hna n x y
  exact ‚ü®m, hm_lt, rpow_le_rpow (apply_nonneg f _) hm (one_div_cast_nonneg (n : ‚Ñï))‚ü©

/-- Auxiliary sequence for the proof that `smoothingSeminorm'` is nonarchimedean. -/
private def mu {x y : R} (hn : ‚àÄ n : ‚Ñï, ‚àÉ (m : ‚Ñï) (_ : m ‚àà Finset.range (n + 1)),
      f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§ (f (x ^ m) * f (y ^ (n - m : ‚Ñï))) ^ (1 / (n : ‚Ñù))) :
    ‚Ñï ‚Üí ‚Ñï := fun n => Classical.choose (hn n)

private theorem mu_property {x y : R} (hn : ‚àÄ n : ‚Ñï, ‚àÉ (m : ‚Ñï) (_hm : m ‚àà Finset.range (n + 1)),
      f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§ (f (x ^ m) * f (y ^ (n - m : ‚Ñï))) ^ (1 / (n : ‚Ñù)))
    (n : ‚Ñï) : f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§
      (f (x ^ mu f hn n) * f (y ^ (n - mu f hn n : ‚Ñï))) ^ (1 / (n : ‚Ñù)) :=
  Classical.choose_spec (Classical.choose_spec (hn n))

private theorem mu_le {x y : R} (hn : ‚àÄ n : ‚Ñï, ‚àÉ (m : ‚Ñï) (_hm : m ‚àà Finset.range (n + 1)),
      f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§ (f (x ^ m) * f (y ^ (n - m : ‚Ñï))) ^ (1 / (n : ‚Ñù)))
    (n : ‚Ñï) : mu f hn n ‚â§ n := by
  simp only [mu, ‚Üê Nat.lt_succ_iff, ‚Üê Finset.mem_range]
  exact Classical.choose (Classical.choose_spec (hn n))

private theorem mu_bdd {x y : R} (hn : ‚àÄ n : ‚Ñï, ‚àÉ (m : ‚Ñï) (_hm : m ‚àà Finset.range (n + 1)),
      f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§ (f (x ^ m) * f (y ^ (n - m : ‚Ñï))) ^ (1 / (n : ‚Ñù)))
    (n : ‚Ñï) : (mu f hn n : ‚Ñù) / n ‚àà Set.Icc (0 : ‚Ñù) 1 := by
  refine Set.mem_Icc.mpr ‚ü®div_nonneg (cast_nonneg (mu f hn n)) (cast_nonneg n), ?_‚ü©
  by_cases hn0 : n = 0
  ¬∑ rw [hn0, cast_zero, div_zero]; exact zero_le_one
  ¬∑ have hn' : 0 < (n : ‚Ñù) := cast_pos.mpr (Nat.pos_of_ne_zero hn0)
    rw [div_le_one hn', cast_le]
    exact mu_le _ _ _

private theorem f_bddBelow (s : ‚Ñï ‚Üí ‚Ñï) {x y : R}
    (_hn : ‚àÄ n : ‚Ñï, ‚àÉ (m : ‚Ñï) (_hm : m ‚àà Finset.range (n + 1)),
      f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§ (f (x ^ m) * f (y ^ (n - m : ‚Ñï))) ^ (1 / (n : ‚Ñù)))
    (œà : ‚Ñï ‚Üí ‚Ñï) :
    BddBelow {a : ‚Ñù | ‚àÄ·∂† n : ‚Ñù in map (fun n : ‚Ñï => f x ^ (‚Üë(s (œà n)) * (1 / (œà n : ‚Ñù)))) atTop,
      n ‚â§ a} := by
  use 0
  simp only [mem_lowerBounds, eventually_map, eventually_atTop, ge_iff_le, Set.mem_setOf_eq,
    forall_exists_index]
  intro r m hm
  exact le_trans (rpow_nonneg (apply_nonneg f _) _) (hm m (le_refl _))

private theorem f_bddAbove (hf1 : f 1 ‚â§ 1) {s : ‚Ñï ‚Üí ‚Ñï} (hs : ‚àÄ n : ‚Ñï, s n ‚â§ n) (x : R)
    (œà : ‚Ñï ‚Üí ‚Ñï) : BddAbove (Set.range fun n : ‚Ñï => f (x ^ s (œà n)) ^ (1 / (œà n : ‚Ñù))) := by
  have hœà : ‚àÄ n : ‚Ñï, 0 ‚â§ 1 / (œà n : ‚Ñù) := fun _ ‚Ü¶ by simp only [one_div, inv_nonneg, cast_nonneg]
  by_cases hx : f x ‚â§ 1
  ¬∑ use 1
    simp only [mem_upperBounds, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff']
    rintro _ n rfl
    apply le_trans (rpow_le_rpow (apply_nonneg _ _) (map_pow_le_pow' hf1 _ _) (hœà n))
    rw [‚Üê rpow_natCast, ‚Üê rpow_mul (apply_nonneg _ _), mul_one_div]
    exact rpow_le_one (apply_nonneg _ _) hx (div_nonneg (cast_nonneg _) (cast_nonneg _))
  ¬∑ use f x
    simp only [mem_upperBounds, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff']
    rintro _ n rfl
    apply le_trans (rpow_le_rpow (apply_nonneg _ _) (map_pow_le_pow' hf1 _ _) (hœà n))
    rw [‚Üê rpow_natCast, ‚Üê rpow_mul (apply_nonneg _ _), mul_one_div]
    conv_rhs => rw [‚Üê rpow_one (f x)]
    rw [rpow_le_rpow_left_iff (not_le.mp hx)]
    exact div_le_one_of_le (cast_le.mpr (hs (œà n))) (cast_nonneg _)

private theorem f_nonempty {s : ‚Ñï ‚Üí ‚Ñï} (hs_le : ‚àÄ n : ‚Ñï, s n ‚â§ n) {x y : R} (_hn : ‚àÄ n : ‚Ñï,
      ‚àÉ (m : ‚Ñï) (_hm : m ‚àà Finset.range (n + 1)),
        f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§ (f (x ^ m) * f (y ^ (n - m : ‚Ñï))) ^ (1 / (n : ‚Ñù)))
    (œà : ‚Ñï ‚Üí ‚Ñï) :
    {a : ‚Ñù | ‚àÄ·∂† n : ‚Ñù in map (fun n : ‚Ñï => f x ^ (‚Üë(s (œà n)) * (1 / (œà n : ‚Ñù)))) atTop,
      n ‚â§ a}.Nonempty := by
  by_cases hfx : f x < 1
  ¬∑ use 1
    simp only [eventually_map, eventually_atTop, ge_iff_le, Set.mem_setOf_eq]
    exact ‚ü®0, fun _ _ ‚Ü¶ rpow_le_one (apply_nonneg _ _) (le_of_lt hfx)
      (mul_nonneg (cast_nonneg _) (one_div_nonneg.mpr (cast_nonneg _)))‚ü©
  ¬∑ use f x
    simp only [eventually_map, eventually_atTop, ge_iff_le, Set.mem_setOf_eq]
    use 0
    intro b _
    nth_rw 2 [‚Üê rpow_one (f x)]
    apply rpow_le_rpow_of_exponent_le (not_lt.mp hfx)
    rw [mul_one_div]
    exact div_le_one_of_le (cast_le.mpr (hs_le (œà b))) (cast_nonneg _)

private theorem f_limsup_le_one {s : ‚Ñï ‚Üí ‚Ñï} (hs_le : ‚àÄ n : ‚Ñï, s n ‚â§ n) {x y : R}
    (hn : ‚àÄ n : ‚Ñï, ‚àÉ (m : ‚Ñï) (_hm : m ‚àà Finset.range (n + 1)),
      f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§ (f (x ^ m) * f (y ^ (n - m : ‚Ñï))) ^ (1 / (n : ‚Ñù)))
      {œà : ‚Ñï ‚Üí ‚Ñï} (hœà_lim : Tendsto ((fun n : ‚Ñï => ‚Üë(s n) / (n : ‚Ñù)) ‚àò œà) atTop (ùìù 0)) :
    limsup (fun n : ‚Ñï => f x ^ ((s (œà n) : ‚Ñù) * (1 / (œà n : ‚Ñù)))) atTop ‚â§ 1 := by
  simp only [limsup, limsSup]
  rw [csInf_le_iff (f_bddBelow f s hn œà) (f_nonempty f hs_le hn œà)]
  ¬∑ intro c hc_bd
    simp only [mem_lowerBounds, eventually_map, eventually_atTop, ge_iff_le, Set.mem_setOf_eq,
      forall_exists_index] at hc_bd
    by_cases hfx : f x < 1
    ¬∑ apply hc_bd (1 : ‚Ñù) 0
      intro b _
      exact rpow_le_one (apply_nonneg _ _) (le_of_lt hfx)
          (mul_nonneg (cast_nonneg _) (one_div_nonneg.mpr (cast_nonneg _)))
    ¬∑ have hf_lim : Tendsto (fun n : ‚Ñï => f x ^ (‚Üë(s (œà n)) * (1 / (œà n : ‚Ñù)))) atTop (ùìù 1) := by
        nth_rw 1 [‚Üê rpow_zero (f x)]
        convert Tendsto.rpow tendsto_const_nhds hœà_lim
            (Or.inl (ne_of_gt (lt_of_lt_of_le zero_lt_one (not_lt.mp hfx))))
        ¬∑ simp only [rpow_zero, mul_one_div, Function.comp_apply]
        ¬∑ rw [rpow_zero]
      rw [tendsto_atTop_nhds] at hf_lim
      apply le_of_forall_pos_le_add
      intro Œµ hŒµ
      have h1 : (1 : ‚Ñù) ‚àà Set.Ioo 0 (1 + Œµ) := by
        simp only [Set.mem_Ioo, zero_lt_one, lt_add_iff_pos_right, true_and_iff, hŒµ]
      obtain ‚ü®k, hk‚ü© := hf_lim (Set.Ioo (0 : ‚Ñù) (1 + Œµ)) h1 isOpen_Ioo
      exact hc_bd (1 + Œµ) k fun b hb => le_of_lt (Set.mem_Ioo.mp (hk b hb)).2

theorem smoothingSeminorm'_isLimit_comp (hf1 : f 1 ‚â§ 1) (x : R) {œà : ‚Ñï ‚Üí ‚Ñï}
    (hœà_mono : StrictMono œà) :
    Tendsto (fun n : ‚Ñï => f (x ^ œà n) ^ (1 / œà n : ‚Ñù)) atTop (ùìù (smoothingSeminorm' f x)) :=
  haveI hœà_lim' : Tendsto œà atTop atTop := StrictMono.tendsto_atTop hœà_mono
  (smoothingSeminorm'_isLimit f hf1 x).comp hœà_lim'

private theorem limsup_mu_le (hf1 : f 1 ‚â§ 1) {s : ‚Ñï ‚Üí ‚Ñï} (hs_le : ‚àÄ n : ‚Ñï, s n ‚â§ n) {x y : R}
    (hn : ‚àÄ n : ‚Ñï, ‚àÉ (m : ‚Ñï) (_hm : m ‚àà Finset.range (n + 1)),
      f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§ (f (x ^ m) * f (y ^ (n - m : ‚Ñï))) ^ (1 / (n : ‚Ñù)))
    {a : ‚Ñù} (a_in : a ‚àà Set.Icc (0 : ‚Ñù) 1) {œà : ‚Ñï ‚Üí ‚Ñï} (hœà_mono : StrictMono œà)
    (hœà_lim : Tendsto ((fun n : ‚Ñï => (s n : ‚Ñù) / ‚Üën) ‚àò œà) atTop (ùìù a)) :
    limsup (fun n : ‚Ñï => f (x ^ s (œà n)) ^ (1 / (œà n : ‚Ñù))) atTop ‚â§ smoothingSeminorm' f x ^ a := by
  by_cases ha : a = 0
  ¬∑ rw [ha] at hœà_lim
    calc limsup (fun n : ‚Ñï => f (x ^ s (œà n)) ^ (1 / (œà n : ‚Ñù))) atTop ‚â§
          limsup (fun n : ‚Ñï => f x ^ ((s (œà n) : ‚Ñù) * (1 / (œà n : ‚Ñù)))) atTop := by
          apply csInf_le_csInf _ (f_nonempty f hs_le hn œà)
          ¬∑ intro b hb
            simp only [eventually_map, eventually_atTop, ge_iff_le, Set.mem_setOf_eq] at hb ‚ä¢
            obtain ‚ü®m, hm‚ü© := hb
            use m
            intro k hkm
            apply le_trans _ (hm k hkm)
            rw [rpow_mul (apply_nonneg f x), rpow_natCast]
            exact rpow_le_rpow (apply_nonneg f _) (map_pow_le_pow' hf1 x _)
              (one_div_nonneg.mpr (cast_nonneg _))
          ¬∑ use 0
            simp only [mem_lowerBounds, eventually_map, eventually_atTop, ge_iff_le,
              Set.mem_setOf_eq, forall_exists_index]
            exact fun _ m hm ‚Ü¶ le_trans (rpow_nonneg (apply_nonneg f _) _) (hm m (le_refl _))
      _ ‚â§ 1 := (f_limsup_le_one f hs_le hn hœà_lim)
      _ = smoothingSeminorm' f x ^ a := by rw [ha, rpow_zero]
  ¬∑ have ha_pos : 0 < a := lt_of_le_of_ne a_in.1 (Ne.symm ha)
    have h_eq : (fun n : ‚Ñï =>
        (f (x ^ s (œà n)) ^ (1 / (s (œà n) : ‚Ñù))) ^ ((s (œà n) : ‚Ñù) / (œà n : ‚Ñù))) =·∂†[atTop]
        fun n : ‚Ñï => f (x ^ s (œà n)) ^ (1 / (œà n : ‚Ñù)) := by
      have h : (fun n : ‚Ñï => (1 : ‚Ñù) / (s (œà n) : ‚Ñù) * (s (œà n) : ‚Ñù)) =·∂†[atTop] 1 := by
        convert div_mul_eventually_cancel 1 (Tendsto.num hœà_mono.tendsto_atTop ha_pos hœà_lim)
          using 1
        ¬∑ simp only [Pi.one_apply, cast_one]
        ¬∑ simp only [Pi.one_apply, cast_one]; rfl
      simp_rw [‚Üê rpow_mul (apply_nonneg f _), mul_div]
      exact EventuallyEq.comp‚ÇÇ EventuallyEq.rfl HPow.hPow (h.div EventuallyEq.rfl)
    exact le_of_eq (Tendsto.limsup_eq (Tendsto.congr' h_eq (Tendsto.rpow
      ((smoothingSeminorm'_isLimit f hf1 x).comp (Tendsto.num hœà_mono.tendsto_atTop ha_pos hœà_lim))
      hœà_lim (Or.inr ha_pos))))

/-- If `f 1 ‚â§ 1` and `f` is nonarchimedean, then `smoothingSeminorm'` is nonarchimedean. -/
theorem smoothingSeminorm_isNonarchimedean (hf1 : f 1 ‚â§ 1) (hna : IsNonarchimedean f) :
    IsNonarchimedean (smoothingSeminorm' f) := by
  intro x y
  have hn : ‚àÄ n : ‚Ñï, ‚àÉ (m : ‚Ñï) (_hm : m ‚àà Finset.range (n + 1)),
        f ((x + y) ^ (n : ‚Ñï)) ^ (1 / (n : ‚Ñù)) ‚â§ (f (x ^ m) * f (y ^ (n - m : ‚Ñï))) ^ (1 / (n : ‚Ñù)) :=
    fun n => exists_index_le f hna x y n
  set mu : ‚Ñï ‚Üí ‚Ñï := fun n => mu f hn n
  set nu : ‚Ñï ‚Üí ‚Ñï := fun n => n - mu n with hnu
  have hmu_le : ‚àÄ n : ‚Ñï, mu n ‚â§ n := fun n => mu_le f hn n
  have hmu_bdd : ‚àÄ n : ‚Ñï, (mu n : ‚Ñù) / n ‚àà Set.Icc (0 : ‚Ñù) 1 := fun n => mu_bdd f hn n
  have hs : Bornology.IsBounded (Set.Icc (0 : ‚Ñù) 1) := Metric.isBounded_Icc 0 1
  obtain ‚ü®a, a_in, œà, hœà_mono, hœà_lim‚ü© := tendsto_subseq_of_bounded hs hmu_bdd
  rw [closure_Icc] at a_in
  set b := 1 - a with hb
  have hb_lim : Tendsto ((fun n : ‚Ñï => (nu n : ‚Ñù) / ‚Üën) ‚àò œà) atTop (ùìù b) := by
    apply Tendsto.congr' _ (Tendsto.const_sub 1 hœà_lim)
    simp only [EventuallyEq, Function.comp_apply, eventually_atTop, ge_iff_le]
    use 1
    intro m hm
    have h0 : (œà m : ‚Ñù) ‚â† 0 := cast_ne_zero.mpr (_root_.ne_of_gt (lt_of_le_of_lt (_root_.zero_le _)
      (hœà_mono (Nat.pos_of_ne_zero (one_le_iff_ne_zero.mp hm)))))
    rw [‚Üê div_self h0, ‚Üê sub_div, cast_sub (hmu_le _)]
  have b_in : b ‚àà Set.Icc (0 : ‚Ñù) 1 := sub_mem_Icc a_in
  have hnu_le : ‚àÄ n : ‚Ñï, nu n ‚â§ n := fun n => by simp only [hnu, tsub_le_self]
  have hx : limsup (fun n : ‚Ñï => f (x ^ mu (œà n)) ^ (1 / (œà n : ‚Ñù))) atTop ‚â§
      smoothingSeminorm' f x ^ a := limsup_mu_le f hf1 hmu_le hn a_in hœà_mono hœà_lim
  have hy : limsup (fun n : ‚Ñï => f (y ^ nu (œà n)) ^ (1 / (œà n : ‚Ñù))) atTop ‚â§
      smoothingSeminorm' f y ^ b :=
    limsup_mu_le f hf1 hnu_le (exists_index_le f hna y x) b_in hœà_mono hb_lim
  have hxy : limsup
      (fun n : ‚Ñï => f (x ^ mu (œà n)) ^ (1 / (œà n : ‚Ñù)) * f (y ^ nu (œà n)) ^ (1 / (œà n : ‚Ñù))) atTop ‚â§
      smoothingSeminorm' f x ^ a * smoothingSeminorm' f y ^ b := by
    have hxy' :
      limsup (fun n : ‚Ñï => f (x ^ mu (œà n)) ^ (1 / (œà n : ‚Ñù)) * f (y ^ nu (œà n)) ^ (1 / (œà n : ‚Ñù)))
        atTop ‚â§ limsup (fun n : ‚Ñï => f (x ^ mu (œà n)) ^ (1 / (œà n : ‚Ñù))) atTop *
          limsup (fun n : ‚Ñï => f (y ^ nu (œà n)) ^ (1 / (œà n : ‚Ñù))) atTop :=
      limsup_mul_le (f_bddAbove f hf1 hmu_le x œà) (fun n => rpow_nonneg (apply_nonneg _ _) _)
        (f_bddAbove f hf1 hnu_le y œà) fun n => rpow_nonneg (apply_nonneg _ _) _
    have h_bdd : IsBoundedUnder LE.le atTop fun n : ‚Ñï => f (y ^ nu (œà n)) ^ (1 / (œà n : ‚Ñù)) :=
      isBoundedUnder f hf1 hnu_le œà
    exact le_trans hxy' (mul_le_mul hx hy (limsup_nonneg_of_nonneg h_bdd fun m =>
      rpow_nonneg (apply_nonneg _ _) _) (rpow_nonneg (smoothingSeminorm_nonneg f hf1 x) _))
  conv_lhs => simp only [smoothingSeminorm']
  apply le_of_forall_sub_le
  intro Œµ hŒµ
  rw [sub_le_iff_le_add]
  have h_mul : smoothingSeminorm' f x ^ a * smoothingSeminorm' f y ^ b + Œµ ‚â§
      max (smoothingSeminorm' f x) (smoothingSeminorm' f y) + Œµ :=  by
    rw [max_def]
    split_ifs with h
    ¬∑ rw [add_le_add_iff_right]
      apply le_trans (mul_le_mul_of_nonneg_right
        (rpow_le_rpow (smoothingSeminorm_nonneg f hf1 _) h a_in.1)
        (rpow_nonneg (smoothingSeminorm_nonneg f hf1 _) _))
      rw [hb, ‚Üê rpow_add_of_nonneg (smoothingSeminorm_nonneg f hf1 _) a_in.1
        (sub_nonneg.mpr a_in.2), add_sub, add_sub_cancel_left, rpow_one]
    ¬∑ rw [add_le_add_iff_right]
      apply le_trans (mul_le_mul_of_nonneg_left
        (rpow_le_rpow (smoothingSeminorm_nonneg f hf1 _) (le_of_lt (not_le.mp h)) b_in.1)
        (rpow_nonneg (smoothingSeminorm_nonneg f hf1 _) _))
      rw [hb, ‚Üê rpow_add_of_nonneg (smoothingSeminorm_nonneg f hf1 _) a_in.1
        (sub_nonneg.mpr a_in.2), add_sub, add_sub_cancel_left, rpow_one]
  apply le_trans _ h_mul
  have hex : ‚àÉ n : PNat, f (x ^ mu (œà n)) ^ (1 / (œà n : ‚Ñù)) * f (y ^ nu (œà n)) ^ (1 / (œà n : ‚Ñù)) <
      smoothingSeminorm' f x ^ a * smoothingSeminorm' f y ^ b + Œµ :=
    exists_lt_of_limsup_le (range_bddAbove_mul (f_bddAbove f hf1 hmu_le _ _)
        (fun n => rpow_nonneg (apply_nonneg _ _) _) (f_bddAbove f hf1 hnu_le _ _)
        fun n => rpow_nonneg (apply_nonneg _ _) _).isBoundedUnder_of_range hxy hŒµ
  obtain ‚ü®N, hN‚ü© := hex
  apply le_trans (ciInf_le (smoothingSeminorm_seq_bddBelow f _)
    ‚ü®œà N, lt_of_le_of_lt (_root_.zero_le (œà 0)) (hœà_mono.lt_iff_lt.mpr N.pos)‚ü©)
  apply le_trans _ hN.le
  simp only [PNat.mk_coe, hnu, ‚Üê mul_rpow (apply_nonneg f _) (apply_nonneg f _)]
  exact mu_property f hn (œà N)

end IsNonarchimedean

/-- If `f 1 ‚â§ 1` and `f` is nonarchimedean, then `smoothingSeminorm'` is a ring seminorm. -/
def smoothingSeminorm (hf1 : f 1 ‚â§ 1) (hna : IsNonarchimedean f) : RingSeminorm R where
  toFun     := smoothingSeminorm' f
  map_zero' := by
    apply tendsto_nhds_unique_of_eventuallyEq (smoothingSeminorm'_isLimit f hf1 0)
      tendsto_const_nhds
    simp only [EventuallyEq, eventually_atTop, ge_iff_le]
    use 1
    intro n hn
    simp only [smoothingSeminormSeq]
    rw [zero_pow (pos_iff_ne_zero.mp hn), map_zero, zero_rpow]
    exact one_div_ne_zero (cast_ne_zero.mpr (one_le_iff_ne_zero.mp hn))
  add_le' := add_le_of_isNonarchimedean (smoothingSeminorm_nonneg f hf1)
      (smoothingSeminorm_isNonarchimedean f hf1 hna)
  neg' n := by
    simp only [smoothingSeminorm', smoothingSeminorm']
    congr
    ext n
    rw [neg_pow]
    rcases neg_one_pow_eq_or R n with hpos | hneg
    ¬∑ rw [hpos, one_mul]
    ¬∑ rw [hneg, neg_one_mul, map_neg_eq_map f]
  mul_le' x y := by
    apply le_of_tendsto_of_tendsto' (smoothingSeminorm'_isLimit f hf1 (x * y))
      (Tendsto.mul (smoothingSeminorm'_isLimit f hf1 x) (smoothingSeminorm'_isLimit f hf1 y))
    intro n
    have hn : 0 ‚â§ 1 / (n : ‚Ñù) := by simp only [one_div, inv_nonneg, cast_nonneg]
    simp only [smoothingSeminormSeq]
    rw [‚Üê mul_rpow (apply_nonneg f _) (apply_nonneg f _), mul_pow]
    exact rpow_le_rpow (apply_nonneg f _) (map_mul_le_mul f _ _) hn

/-- If `f 1 ‚â§ 1` and `f` is nonarchimedean, then `smoothingSeminorm f hf1 hna 1 ‚â§ 1`. -/
theorem smoothingSeminorm_is_seminorm_is_norm_le_one_class (hf1 : f 1 ‚â§ 1)
    (hna : IsNonarchimedean f) : smoothingSeminorm f hf1 hna 1 ‚â§ 1 :=
  smoothingSeminorm_one f hf1

/-- If `f 1 ‚â§ 1` and `f` is nonarchimedean, then `smoothingSeminorm' f` is
  power-multiplicative. -/
theorem smoothingSeminorm_isPowMul (hf1 : f 1 ‚â§ 1) : IsPowMul (smoothingSeminorm' f) := by
  intro x m hm
  simp only [smoothingSeminorm']
  have hlim : Tendsto (fun n => smoothingSeminormSeq f x (m * n)) atTop
      (ùìù (smoothingSeminorm' f x)) :=
    Tendsto.comp (smoothingSeminorm'_isLimit f hf1 x) (tendsto_atTop_atTop_of_monotone
      (fun n k hnk ‚Ü¶ mul_le_mul_left' hnk m) (fun n ‚Ü¶ ‚ü®n, le_mul_of_one_le_left' hm‚ü©))
  apply tendsto_nhds_unique _ (Tendsto.pow hlim m)
  have h_eq : ‚àÄ n : ‚Ñï, smoothingSeminormSeq f x (m * n) ^ m = smoothingSeminormSeq f (x ^ m) n := by
    have hm' : (m : ‚Ñù) ‚â† 0 := cast_ne_zero.mpr (_root_.ne_of_gt (lt_of_lt_of_le zero_lt_one hm))
    intro n
    simp only [smoothingSeminormSeq]
    rw [pow_mul, ‚Üê rpow_natCast, ‚Üê rpow_mul (apply_nonneg f _), cast_mul, ‚Üê one_div_mul_one_div,
      mul_comm (1 / (m : ‚Ñù)), mul_assoc, one_div_mul_cancel hm', mul_one]
  simp_rw [h_eq]
  exact smoothingSeminorm'_isLimit f hf1 _

/-- If `f 1 ‚â§ 1` and `‚àÄ (1 ‚â§ n), f (x ^ n) = f x ^ n`, then `smoothingSeminorm' f x = f x`. -/
theorem smoothingSeminorm_of_powMul (hf1 : f 1 ‚â§ 1) {x : R}
    (hx : ‚àÄ (n : ‚Ñï) (_hn : 1 ‚â§ n), f (x ^ n) = f x ^ n) : smoothingSeminorm' f x = f x := by
  apply tendsto_nhds_unique_of_eventuallyEq (smoothingSeminorm'_isLimit f hf1 x)
    tendsto_const_nhds
  simp only [EventuallyEq, eventually_atTop, ge_iff_le]
  use 1
  intro n hn
  simp only [smoothingSeminormSeq]
  have hn0 : (n : ‚Ñù) ‚â† 0 := cast_ne_zero.mpr (one_le_iff_ne_zero.mp hn)
  rw [hx n hn, ‚Üê rpow_natCast, ‚Üê rpow_mul (apply_nonneg f _), mul_one_div_cancel hn0, rpow_one]

/-- If `f 1 ‚â§ 1` and `‚àÄ y : R, f (x * y) = f x * f y`, then `smoothingSeminorm' f x = f x`. -/
theorem smoothingSeminorm_apply_of_is_mul' (hf1 : f 1 ‚â§ 1) {x : R}
    (hx : ‚àÄ y : R, f (x * y) = f x * f y) : smoothingSeminorm' f x = f x := by
  apply tendsto_nhds_unique_of_eventuallyEq (smoothingSeminorm'_isLimit f hf1 x) tendsto_const_nhds
  simp only [EventuallyEq, eventually_atTop, ge_iff_le]
  use 1
  intro n hn
  simp only [smoothingSeminormSeq]
  by_cases hx0 : f x = 0
  ¬∑ have hxn : f (x ^ n) = 0 := by
      apply le_antisymm _ (apply_nonneg f _)
      apply le_trans (map_pow_le_pow f x (one_le_iff_ne_zero.mp hn))
      rw [hx0, zero_pow (pos_iff_ne_zero.mp hn)]
    rw [hx0, hxn, zero_rpow (one_div_cast_ne_zero (one_le_iff_ne_zero.mp hn))]
  ¬∑ have h1 : f 1 = 1 := by rw [‚Üê mul_right_inj' hx0, ‚Üê hx 1, mul_one, mul_one]
    have hn0 : (n : ‚Ñù) ‚â† 0 := cast_ne_zero.mpr (_root_.ne_of_gt (lt_of_lt_of_le zero_lt_one hn))
    rw [‚Üê mul_one (x ^ n), is_mul_pow_of_is_mul f hx, ‚Üê rpow_natCast, h1, mul_one, ‚Üê
      rpow_mul (apply_nonneg f _), mul_one_div_cancel hn0, rpow_one]

/-- If `f 1 ‚â§ 1`, `f` is nonarchimedean, and `‚àÄ y : R, f (x * y) = f x * f y`, then
  `smoothingSeminorm f hf1 hna x = f x`. -/
theorem smoothingSeminorm_apply_of_is_mul (hf1 : f 1 ‚â§ 1) (hna : IsNonarchimedean f) {x : R}
    (hx : ‚àÄ y : R, f (x * y) = f x * f y) : smoothingSeminorm f hf1 hna x = f x :=
  smoothingSeminorm_apply_of_is_mul' f hf1 hx

/-- If `f 1 ‚â§ 1`, and `x` is multiplicative for `f`, then it is multiplicative for
  `smoothingSeminorm'`. -/
theorem smoothingSeminorm_of_mul' (hf1 : f 1 ‚â§ 1) {x : R} (hx : ‚àÄ y : R, f (x * y) = f x * f y)
    (y : R) : smoothingSeminorm' f (x * y) = smoothingSeminorm' f x * smoothingSeminorm' f y := by
  have hlim : Tendsto (fun n => f x * smoothingSeminormSeq f y n) atTop
      (ùìù (smoothingSeminorm' f x * smoothingSeminorm' f y)) := by
    rw [smoothingSeminorm_apply_of_is_mul' f hf1 hx]
    exact Tendsto.const_mul _ (smoothingSeminorm'_isLimit f hf1 y)
  apply tendsto_nhds_unique_of_eventuallyEq (smoothingSeminorm'_isLimit f hf1 (x * y)) hlim
  simp only [EventuallyEq, eventually_atTop, ge_iff_le]
  use 1
  intro n hn1
  have hn0 : (n : ‚Ñù) ‚â† 0 := Nat.cast_ne_zero.mpr (_root_.ne_of_gt (lt_of_lt_of_le zero_lt_one hn1))
  simp only [smoothingSeminormSeq]
  rw [mul_pow, is_mul_pow_of_is_mul f hx,
    mul_rpow (pow_nonneg (apply_nonneg f _) _) (apply_nonneg f _), ‚Üê rpow_natCast, ‚Üê
    rpow_mul (apply_nonneg f _), mul_one_div_cancel hn0, rpow_one]

/-- If `f 1 ‚â§ 1`, `f` is nonarchimedean, and `x` is multiplicative for `f`, then `x` is
  multiplicative for `smoothingSeminorm`. -/
theorem smoothingSeminorm_of_mul (hf1 : f 1 ‚â§ 1) (hna : IsNonarchimedean f) {x : R}
    (hx : ‚àÄ y : R, f (x * y) = f x * f y) (y : R) :
    smoothingSeminorm f hf1 hna (x * y) =
      smoothingSeminorm f hf1 hna x * smoothingSeminorm f hf1 hna y :=
  smoothingSeminorm_of_mul' f hf1 hx y

end smoothingSeminorm
